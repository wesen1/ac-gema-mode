//
// @author wesen
// @copyright 2019 wesen <wesen-ac@web.de>
//

//
// Map record loader module for ac-gema-mode
//
// Handles loading of map records for specified maps
//
// Requires:
//   * scripts/ac-gema-mode/map-record/storage/map-record-storage.cfg
//

persistidents 0;


// Public Functions

//
// Loads and returns a map record for a specified map with a specific weapon.
//
// @param string mapName ($arg1) The map name
// @param int weaponId ($arg2) The weapon id
//
// @return array The map record or an empty string if no record was found
//
const loadWeaponMapRecord [
  return (readMapRecordFromSavedCfg $arg1 $arg2);
]

//
// Returns the best map record for a specified map.
// Will return an empty string if there is no map record for the target map.
//
// @param string mapName ($arg1) The map name
//
// @return array The best map record
//
const loadBestMapRecord [

  mapName = $arg1;

  bestMapRecord = "";
  bestMapRecordScoreTime = -1;

  looplist $allWeaponIds weaponId [

    mapRecord = (loadWeaponMapRecord $mapName $weaponId);

    if (! (strcmp $mapRecord "")) [

      mapRecordScoreTime = (getMapRecordScoreTime $mapRecord);
      if (|| (strcmp $bestMapRecord "") (> $bestMapRecordScoreTime $mapRecordScoreTime)) [
        // No best map record was found yet or this map record is faster than the current one
        bestMapRecord = $mapRecord;
        bestMapRecordScoreTime = $mapRecordScoreTime;
      ]
    ]

  ]

  return $bestMapRecord;

]
