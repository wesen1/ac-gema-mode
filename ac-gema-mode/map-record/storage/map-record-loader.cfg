//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

//
// Map record loader module for ac-gema-mode
//
// Handles loading of map records for specified maps.
//
// Requires:
//   * scripts/ac-gema-mode/map-record/map-record-comparator.cfg
//   * scripts/ac-gema-mode/map-record/storage/map-record-storage.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//   * scripts/ac-gema-mode/unpersist/functions.cfg
//

persistidents 0;


// Public Functions

//
// Loads and returns a map record for a specified map with a specific weapon.
//
// @param string mapName ($arg1) The map name
// @param int weaponId ($arg2) The weapon id
// @param int mapRevision ($arg3) The map revision
//
// @return array The map record or an empty string if no record was found
//
function loadWeaponMapRecord [
  return (readMapRecordFromSavedCfg $arg1 $arg2 $arg3);
]

//
// Returns the best map record for a specified map.
// Will return an empty string if there is no map record for the target map.
//
// @param string mapName ($arg1) The map name
// @param int mapRevision ($arg2) The map revision
//
// @return array The best map record
//
function loadBestMapRecord [

  mapName = $arg1;
  mapRevision = $arg2;

  bestMapRecord = "";
  ulooplist $allWeaponIds weaponId [

    loadedWeaponMapRecord = (loadWeaponMapRecord $mapName $weaponId $mapRevision);

    if (! (strcmp $loadedWeaponMapRecord "")) [
      // Found a map record for the weapon

      if (strcmp $bestMapRecord "") [
        // This is the first map record that was found so far
        bestMapRecord = $loadedWeaponMapRecord;
      ][
        mapRecordComparison = (compareMapRecords $loadedWeaponMapRecord $bestMapRecord);
        comparisonStatus = (at $mapRecordComparison 0);

        if (= $comparisonStatus $MAP_RECORD_COMPARISON_STATUS_FASTER) [
          bestMapRecord = $loadedWeaponMapRecord;
        ]
      ]

    ]

  ]

  return $bestMapRecord;

]
