//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

//
// Map record saver module for ac-gema-mode
//
// Handles saving of map records for specified maps.
//
// Requires:
//   * scripts/ac-gema-mode/map-record/storage/map-record-storage.cfg
//   * scripts/ac-gema-mode/map-record/map-record-comparator.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//

persistidents 0;


// Public Functions

//
// Saves a map record if it is better than the previous best record with the map record's score weapon.
// The map record times are saved in a persistent variable per map.
//
// @param array mapRecord ($arg1) The map record
// @param int doWriteCfg ($arg2) 1 to call writecfg if the map records gets saved, 0 otherwise (optional, Default: 1)
//
function saveMapRecord [

  mapRecord = $arg1;
  if (= $numargs 2) [
    doWriteCfg = $arg2;
  ][
    doWriteCfg = 1;
  ]

  comparisonToWeaponMapRecord = (compareMapRecordToWeaponRecord $mapRecord);
  comparisonStatus = (at $comparisonToWeaponMapRecord 0);

  isNewBestWeaponMapRecord = (|| (= $comparisonStatus $MAP_RECORD_COMPARISON_STATUS_FASTER) (= $comparisonStatus $MAP_RECORD_COMPARISON_STATUS_EMPTY));

  if ($isNewBestWeaponMapRecord) [
    // There is no record for that weapon or the new record is faster than the existing one with that weapon
    writeMapRecordToSavedCfg $mapRecord $doWriteCfg;
  ]

]
