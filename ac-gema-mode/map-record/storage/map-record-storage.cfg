//
// @author wesen
// @copyright 2017-2020 wesen <wesen-ac@web.de>
//

//
// Map record storage module for ac-gema-mode
//
// Handles the reading and writing of map records from and to the saved.cfg file.
// The records are saved in a two dimensional array per map.
// The structure of these arrays is: [ <weapon id> => <score time>, ... ]
//
// Requires:
//   * scripts/ac-gema-mode/map-record/map-record-array.cfg
//   * scripts/ac-gema-mode/util/array-utils.cfg
//

persistidents 0;


// Public Functions

//
// Writes a map record to the saved.cfg file.
//
// @param array mapRecord ($arg1) The map record to write
//
const writeMapRecordToSavedCfg [

  mapRecord = $arg1;
  mapRecordScoreTime = (getMapRecordScoreTime $mapRecord);
  mapRecordWeaponId = (getMapRecordWeaponId $mapRecord);

  mapRecordListAlias = (getMapRecordListAlias (getMapRecordMapName $mapRecord));
  mapRecordList = (loadMapRecordList $mapRecordListAlias);
  updatedMapRecordList = (array_set $mapRecordList (concat $mapRecordWeaponId 0) $mapRecordScoreTime);

  writeUpdatedMapRecordListToSavedCfg $mapRecordListAlias $updatedMapRecordList;
  onMapRecordSaved $mapRecord;

]

//
// Loads and returns a map record for a specified map with a specific weapon from the saved.cfg file.
//
// @param string mapName ($arg1) The map name
// @param int weaponId ($arg2) The weapon id
//
// @return array The map record or an empty string if no record was found
//
const readMapRecordFromSavedCfg [

  mapName = $arg1;
  weaponId = $arg2;

  mapRecordListAlias = (getMapRecordListAlias $mapName);

  // Fetch the score time from the list of map records
  mapRecordList = (loadMapRecordList $mapRecordListAlias);
  mapRecordScoreTime = (array_get $mapRecordList $weaponId);

  if (! (strcmp $mapRecordScoreTime "")) [
    return (createMapRecordArray $mapName $mapRecordScoreTime $weaponId);
  ]

]

//
// Deletes a map record from saved.cfg.
//
// @param string mapName ($arg1) The map name of the record to delete
// @param int weaponId ($arg2) The weapon id of the record to delete
//
const deleteMapRecordFromSavedCfg [

  mapName = $arg1;
  weaponId = $arg2;

  mapRecord = (readMapRecordFromSavedCfg $mapName $weaponId);
  if (! (strcmp $mapRecord "")) [
    mapRecordListAlias = (getMapRecordListAlias $mapName);
    mapRecordList = (loadMapRecordList $mapRecordListAlias);
    updatedMapRecordList = (array_set $mapRecordList $weaponId "");

    writeUpdatedMapRecordListToSavedCfg $mapRecordListAlias $updatedMapRecordList;
    printGemaNotification $messageRecordDeleted;
    onWeaponRecordDeleted $mapRecord;
  ]

]

//
// Deletes all map records for a specified map.
//
// @param string mapName ($arg1) The map name
//
const deleteAllMapRecordsOfMapFromSavedCfg [

  mapName = $arg1;
  mapRecordListAlias = (getMapRecordListAlias $mapName);
  if (isIdent $mapRecordListAlias)[
    writeUpdatedMapRecordListToSavedCfg $mapRecordListAlias "";
    onAllRecordsOfMapDeleted $mapName;
  ]

]


// Private Functions

//
// Returns the map record list alias name for a specific map.
// This is the name of the variable in which the records for the map are saved.
//
// @param string mapName ($arg1) The map name
//
// @return string The map record list alias
//
// @example `getMapRecordListAlias gibbed-gema11` Returns ::gema_records_gibbed-gema11
//
const getMapRecordListAlias [
  return (concatword "::gema_records_" $arg1);
]

//
// Loads and returns a map record list from a given map record list alias name.
//
// @param string mapRecordListAlias ($arg1) The map record list alias name
//
// @return array The map record list or an empty string if no map record list with that alias name exists
//
const loadMapRecordList [
  mapRecordListAlias = $arg1;
  if (isIdent $mapRecordListAlias) [
    return (getalias $mapRecordListAlias);
  ]
]

//
// Writes a updated map record list to the saved.cfg file.
//
// @param string mapRecordListAlias ($arg1) The map record list alias name
// @param array updatedMapRecordList ($arg2) The updated map record list
//
const writeUpdatedMapRecordListToSavedCfg [

  mapRecordListAlias = $arg1;
  updatedMapRecordList = $arg2;

  // Set persistidents to 1 so that variables that are assigned after this line will be stored
  // in saved.cfg on the next writecfg call
  persistidents 1;

  if (strcmp $updatedMapRecordList "") [
    delalias $mapRecordListAlias;
  ][
    // Set the value for a variable that is named like the string that is stored in $mapRecordListAlias
    $mapRecordListAlias = $updatedMapRecordList;
  ]

  // Manually trigger writecfg to immediately write the updated map record list to saved.cfg
  writecfg;

  // Turn off persistidents
  persistidents 0;

]


// Custom Events

//
// Event handler that is called after a map record was saved.
//
// @param array mapRecord ($arg1) The map record
//
checkinit onMapRecordSaved [
]

//
// Event handler that is called after a weapon map record was deleted.
//
// @param array mapRecord ($arg1) The map record
//
checkinit onWeaponRecordDeleted [
]

//
// Event handler that is called after all records for a map were deleted at once.
//
// @param string mapName ($arg1) The map name
//
checkinit onAllRecordsOfMapDeleted [
]
