//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

//
// Map record array module for ac-gema-mode
//
// Provides functions to create and to extract information from map record information arrays.
// Map record information arrays are in the format array(0 => <mapName>, 1 => <scoreTime>, 2 => <weaponId>)
//
// Requires:
//   * scripts/ac-gema-mode/time/time-interval-array.cfg
//   * scripts/ac-gema-mode/util/weapon-name-fetcher.cfg
//   * scripts/ac-gema-mode/util/array-utils.cfg
//

persistidents 0;


// Public Functions

//
// Creates and returns a map record array.
//
// @param string mapName ($arg1) The map name
// @param int scoreTime ($arg2) The score time in milliseconds
// @param int weaponId ($arg3) The weapon id
//
// @return array The created map record array
//
const createMapRecordArray [

  mapName = $arg1;
  scoreTime = (createTimeIntervalArray $arg2);
  weaponId = $arg3;

  mapRecordArray = (array_set $mapName "1 0" $scoreTime);
  mapRecordArray = (array_set $mapRecordArray "2 0" $weaponId);

  return $mapRecordArray;

]

//
// Returns a map record's map name.
//
// @param array mapRecordArray ($arg1) The map record array
//
// @return string The map record's map name
//
const getMapRecordMapName [
  return (array_get $arg1 "0 0");
]

//
// Returns a map record's score time.
//
// @param array mapRecordArray ($arg1) The map record array
//
// @return int The map record's score time
//
const getMapRecordScoreTime [
  return (array_get $arg1 "1 0");
]

//
// Returns a map record's weapon id.
//
// @param array mapRecordArray ($arg1) The map record array
//
// @return int The map record's weapon id
//
const getMapRecordWeaponId [
  return (array_get $arg1 "2 0");
]

//
// Returns a map record's weapon name.
//
// @param array mapRecordArray ($arg1) The map record array
//
// @return string The weapon name
//
const getMapRecordWeaponName [

  weaponId = (getMapRecordWeaponId $arg1);
  if (strcmp $weaponId "") [
    return "";
  ][
    return (getWeaponName $weaponId);
  ]

]
