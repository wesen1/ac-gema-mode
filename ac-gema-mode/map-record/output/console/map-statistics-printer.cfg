//
// @author wesen
// @copyright 2017-2020 wesen <wesen-ac@web.de>
//

//
// Console map statistics printer module for ac-gema-mode
//
// Handles printing of statistics about a map to the console at the top of the screen.
//
// Requires:
//   * scripts/ac-gema-mode/custom-declarations.cfg
//   * scripts/ac-gema-mode/gema-mode-state/gema-mode-state.cfg
//   * scripts/ac-gema-mode/map-record/map-record-array.cfg
//   * scripts/ac-gema-mode/map-record/output/map-record-output.cfg
//   * scripts/ac-gema-mode/map-record/storage/best-map-record-cache.cfg
//   * scripts/ac-gema-mode/map-record/storage/map-record-loader.cfg
//   * scripts/ac-gema-mode/notification-printer.cfg
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/time/time-formatter.cfg
//   * scripts/ac-gema-mode/time/timestamp-formatter.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//   * scripts/ac-gema-mode/unpersist/functions.cfg
//   * scripts/ac-gema-mode/util/output-utils.cfg
//


// Options

//
// Option to show the list of missing weapons on map start (Default: 1)
//
// @var int $optionShowMissingWeapons
//
option optionShowMissingWeapons 1;

persistidents 0;


// Public Functions

//
// Shows the best record of a map.
// Also shows a list of weapons with which the map was not finished yet if $optionShowMissingWeapons is 1.
//
// @param string mapName ($arg1) The map name
// @param int mapRevision ($arg2) The map revision
//
function printMapStatistics [

  mapName = $arg1;
  mapRevision = $arg2;

  bestMapRecord = (getBestMapRecord $mapName $mapRevision);
  if (strcmp $bestMapRecord "") [
    // There is no map record for the map
    printGemaNotification $messageNoMapRecords;
  ][

    bestMapRecordTeamId = (getMapRecordTeamId $bestMapRecord);

    scoreTimeColors = (getScoreTimeColors $bestMapRecordTeamId);
    bestMapRecordScoreTimeString = (generateTimeStringFromMilliseconds (getMapRecordScoreTime $bestMapRecord) $scoreTimeColors);

    bestMapRecordString = (format $messageBestMapRecordConsole $bestMapRecordScoreTimeString);
    if ($optionShowScoreWeapon) [
      weaponName = (colorizeWeaponName (getMapRecordWeaponName $bestMapRecord) $bestMapRecordTeamId);
      weaponNameInfo = (format $messageBestMapRecordWeaponConsole $weaponName);
      bestMapRecordString = (concat $bestMapRecordString $weaponNameInfo);
    ]

    if (= $optionShowMapRecordTeam 5) [
      teamName = (colorizeTeamName (getMapRecordTeamName $bestMapRecord) $bestMapRecordTeamId);
      teamInfoString = (format $messageBestMapRecordTeamNameConsole $teamName);
      bestMapRecordString = (concat $bestMapRecordString $teamInfoString);
    ]

    if ($optionShowMapRecordTimestamp) [
      bestMapRecordTimestamp = (getMapRecordTimestampArray $bestMapRecord);
      timestampDate = (colorizeTimestampDate (formatTimestampDate $bestMapRecordTimestamp) $bestMapRecordTeamId);

      if ($optionShowMapRecordTimestampTimePart) [
        timestampTime = (colorizeTimestampTime (formatTimestampTime $bestMapRecordTimestamp) $bestMapRecordTeamId);
        timestampInfoString = (format $messageBestMapRecordTimestampDateAndTimeConsole $timestampDate $timestampTime);
      ][
        timestampInfoString = (format $messageBestMapRecordTimestampDateConsole $timestampDate);
      ]

      bestMapRecordString = (concat $bestMapRecordString $timestampInfoString);
    ]

    printGemaNotification $bestMapRecordString;

    if ($optionShowMissingWeapons) [
      missingMapRecordWeaponNames = (getMissingMapRecordWeaponNamesString $mapName $mapRevision);
      if (! (strcmp $missingMapRecordWeaponNames "")) [
        printGemaNotification (format $messageMissingWeapons $missingMapRecordWeaponNames);
      ]
    ]
  ]

]


// Private Functions

//
// Returns a comma separated string of weapons with which the player didn't score on a map.
// Returns an empty string when the map was finished with all weapons.
//
// @param string mapName ($arg1) The map name
// @param int mapRevision ($arg2) The map revision
//
// @return string The missing weapon names string
//
function getMissingMapRecordWeaponNamesString [

  mapName = $arg1;
  mapRevision = $arg2;

  missingWeaponNames = [];
  ulooplist $allWeaponIds weaponId [

    mapRecord = (loadWeaponMapRecord $mapName $weaponId $mapRevision);
    if (strcmp $mapRecord "") [
      // There is no record for the map with that weapon
      add2list missingWeaponNames (strreplace (getWeaponName $weaponId) " " $TEMPORARY_WHITESPACE);
    ]

  ]

  return (generateOutputList $missingWeaponNames $messageMissingWeaponsListSeparator $messageMissingWeaponsListLastSeparator);

]


// Event Handlers

//
// Event handler that is called after the gema mode state auto change checks on map start are complete.
//
// @param int newGemaModeState ($arg1) The new gema mode state
//
eventlistener onMapStartGemaModeStateAutoChangeChecksFinished [
  if (= $arg1 1) [
    printMapStatistics (curmap) $curmaprevision;
  ]
]
