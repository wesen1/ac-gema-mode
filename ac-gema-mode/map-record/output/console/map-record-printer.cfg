//
// @author wesen
// @copyright 2017-2019 wesen <wesen-ac@web.de>
//

//
// Console map record printer module for ac-gema-mode
//
// Handles printing of map records to the console at the top of the screen
//
// Requires:
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/map-record/map-record-array.cfg
//   * scripts/ac-gema-mode/map-record/map-record-comparator.cfg
//   * scripts/ac-gema-mode/util/time-parser.cfg
//


// Options

//
// Option to show the player name in the console score string
//
// @var int $optionAutoActivateGemaMode
//
check2init optionShowNameOnScore 0;

//
// Option to show the score weapon in the console score string
//
// @var int $optionShowScoreWeapon
//
check2init optionShowScoreWeapon 1;

//
// Option to set the comparison value for score times
//
// 0: Best map record
// 1: Best score weapon record
//
check2init optionScoreTimeCompareValue 1;


persistidents 0;

//
// Generates and returns the map record output string for when the player scores.
//
// @param array $arg1 The map record
//
// @return string The map record output string for when the player scores
//
const getMapRecordScoreString [

  mapRecord = $arg1;

  if ($optionShowNameOnScore) [
    scorePlayerName = (format $messageScoreNamePlayerName (player1 name));
  ][
    scorePlayerName = $messageScoreNameYou;
  ]

  scoreTimeString = (generateTimeString (getMapRecordScoreTime $mapRecord) 1);
  mapRecordString = (format $messageMapRecordScore $scorePlayerName $scoreTimeString);

  if ($optionShowScoreWeapon) [
    weaponInfoString = (format $messageMapRecordScoreWeapon (getMapRecordWeaponName $mapRecord));
    mapRecordString = (concat $mapRecordString $weaponInfoString);
  ]

  return $mapRecordString;

]

//
// Returns the record status string (new record, tied record, no new record).
// Must be called before the new record was saved.
//
// @param array $arg1 The map record
//
// @return string The record status string
//
const getMapRecordStatusString [

  newMapRecord = $arg1;

  comparisonToBestMapRecord = (compareMapRecordToBestMapRecord $newMapRecord);
  isNewBestMapRecord = (|| (= $comparisonToBestMapRecord $MAP_RECORD_COMPARISON_STATUS_FASTER) (= $comparisonToBestMapRecord $MAP_RECORD_COMPARISON_STATUS_EMPTY));

  if (|| $isNewBestMapRecord (= $optionScoreTimeCompareValue 0)) [
    comparisonToTargetMapRecord = $comparisonToBestMapRecord;
    isComparisonToBestMapRecord = 1;
  ][
    comparisonToTargetMapRecord = (compareMapRecordToWeaponRecord $newMapRecord);
    isComparisonToBestMapRecord = 0;
  ]


  if (= $comparisonToTargetMapRecord $MAP_RECORD_COMPARISON_STATUS_EMPTY) [
    // This is the first record for the map or with the score weapon
    return (getNewMapRecordString $newMapRecord $isComparisonToBestMapRecord);
  ][

    if (= $comparisonToTargetMapRecord $MAP_RECORD_COMPARISON_STATUS_FASTER) [
      return (getFasterThanCompareMapRecordString $newMapRecord $isComparisonToBestMapRecord);
    ][
      if (= $comparisonToTargetMapRecord $MAP_RECORD_COMPARISON_STATUS_TIED) [
        return (getCompareMapRecordTiedString $newMapRecord $isComparisonToBestMapRecord);
      ][
        return (getSlowerThanCompareMapRecordString $newMapRecord $isComparisonToBestMapRecord);
      ]
    ]

  ]

]


// Private Functions

//
// Returns the "New best time" message for the case that the compare record is empty.
//
// @param array newMapRecord ($arg1) The new map record
// @param int isComparisonToBestMapRecord ($arg2) 1 if the map record was compared to the best map record
//
// @return string The "New best time" message
//
const getNewMapRecordString [

  newMapRecord = $arg1;
  isComparisonToBestMapRecord = $arg2;

  if ($isComparisonToBestMapRecord) [
    return $messageFasterThanBestMapRecord;
  ][
    return (format $messageFasterThanWeaponMapRecord (getMapRecordWeaponName $newMapRecord));
  ]

]

// Returns the "Faster than map record" message.
//
// @param array newMapRecord ($arg1) The new map record
// @param int isComparisonToBestMapRecord ($arg2) 1 if the map record was compared to the best map record
//
// @return string The "Faster than map record" message
//
const getFasterThanCompareMapRecordString [

  newMapRecord = $arg1;
  isComparisonToBestMapRecord = $arg2;

  if ($isComparisonToBestMapRecord) [
    return $messageFasterThanBestMapRecord;
  ][
    weaponName = (getMapRecordWeaponName $newMapRecord);
    return (format $messageFasterThanWeaponMapRecord $weaponName);
  ]

]

//
// Returns the "Tied map record" message.
//
// @param array newMapRecord ($arg1) The new map record
// @param int isComparisonToBestMapRecord ($arg2) 1 if the map record was compared to the best map record
//
// @return string The "Tied map record" message
//
const getCompareMapRecordTiedString [

  newMapRecord = $arg1;
  isComparisonToBestMapRecord = $arg2;

  if ($isComparisonToBestMapRecord) [
    return $messageTiedBestMapRecord;
  ][
    return (format $messageTiedWeaponMapRecord (getMapRecordWeaponName $newMapRecord));
  ]

]

//
// Returns the "Slower than map record" message.
//
// @param array newMapRecord ($arg1) The new map record
// @param int isComparisonToBestMapRecord ($arg2) 1 if the map record was compared to the best map record
//
// @return string The "Slower than map record" message
//
const getSlowerThanCompareMapRecordString [

  newMapRecord = $arg1;
  isComparisonToBestMapRecord = $arg2;

  if ($isComparisonToBestMapRecord) [
    return $messageSlowerThanBestMapRecord;
  ][
    weaponName = (getMapRecordWeaponName $newMapRecord);
    return (format $messageSlowerThanWeaponMapRecord $weaponName);
  ]

]
