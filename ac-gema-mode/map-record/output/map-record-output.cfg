//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Map record output module for ac-gema-mode
//
// Provides options and functions that are shared across the different map record output modules.
//
// Requires:
//   * scripts/ac-gema-mode/colors.cfg
//   * scripts/ac-gema-mode/custom-declarations.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//

// Options

//
// Option to show the score weapon in the console score and statistics strings and the main menu (Default: 1)
//
// @var int $optionShowScoreWeapon
//
option optionShowScoreWeapon 1;

//
// Option to show the player team in the console score and statistics strings and the main menu (Default: 3)
//
// 0: Disabled
// 1: Show player name in team color
// 2: Show time in team color
// 3: Show weapon in team color
// 4: Show timestamp in team color
// 5: Show team name
//
// @var int $optionShowMapRecordTeam
//
option optionShowMapRecordTeam 3;

//
// Option to show the map record timestamps in the map statistics and the menus (Default: 1)
//
// @var int $showMapRecordTimestamp
//
option optionShowMapRecordTimestamp 1;

//
// Option to show the time parts of the map record timestamps (Default: 0)
//
// @var int $optionShowMapRecordTimestampTimePart
//
option optionShowMapRecordTimestampTimePart 0;

persistidents 0;


// Public Functions

//
// Prepends a color to a player name and returns the result.
//
// @param string playerName ($arg1) The player name to colorize
// @param int mapRecordTeamId ($arg2) The team id of the map record to which the player name belongs
//
// @return string The colorized player name
//
function colorizePlayerName [
  return (concatword (getPlayerNameColor $arg2) $arg1);
]

//
// Prepends a color to a weapon name and returns the result.
//
// @param string weaponName ($arg1) The weapon name to colorize
// @param int mapRecordTeamId ($arg2) The team id of the map record to which the weapon name belongs
// @param int highlightAsBestWeaponName ($arg3) 1 to highlight the weapon name as best score weapon
//
// @return string The colorized weapon name
//
function colorizeWeaponName [

  weaponName = $arg1;
  mapRecordTeamId = $arg2;

  if (= $numargs 3) [
    highlightAsBestWeaponName = $arg3;
  ][
    highlightAsBestWeaponName = 0;
  ]

  if (&& $highlightAsBestWeaponName (!= $optionShowMapRecordTeam 3)) [
    return (concat (c $colorBestMapRecordWeapon) $weaponName);
  ][
    return (concatword (getWeaponNameColor $mapRecordTeamId) $weaponName);
  ]

]

//
// Prepends a color to a team name and returns the result.
//
// @param string teamName ($arg1) The team name to colorize
// @param int mapRecordTeamId ($arg2) The team id of the map record to which the team name belongs
//
// @return string The colorized team name
//
function colorizeTeamName [
  return (concatword (getTeamColor $arg2) $arg1);
]

//
// Prepends a color to a timestamp date string and returns the result.
//
// @param string timestampDateString ($arg1) The timestamp date string to colorize
// @param int mapRecordTeamId ($arg2) The team id of the map record to which the timestamp belongs
//
// @return string The colorized timestamp date string
//
function colorizeTimestampDate [
  return (concatword (getTimestampDateColor $arg2) $arg1);
]

//
// Prepends a color to a timestamp time string and returns the result.
//
// @param string timestampTimeString ($arg1) The timestamp time string to colorize
// @param int mapRecordTeamId ($arg2) The team id of the map record to which the timestamp belongs
//
// @return string The colorized timestamp time string
//
function colorizeTimestampTime [
  return (concatword (getTimestampTimeColor $arg2) $arg1);
]

//
// Returns the colors to use for formatting a score time.
//
// @param int mapRecordTeamId ($arg1) The team id of the map record to which the score time belongs
// @param int highlightAsBestScoreTime ($arg2) 1 to highlight the score time as best score time (optional)
//
// @return array The score time colors
//
function getScoreTimeColors [

  mapRecordTeamId = $arg1;

  if (= $numargs 2) [
    highlightAsBestScoreTime = $arg2;
  ][
    highlightAsBestScoreTime = 0;
  ]

  if (= $optionShowMapRecordTeam 2) [

    if (= $mapRecordTeamId $TEAM_CLA) [
      return $colorsRecordTimeCLA;
    ][
      if (= $mapRecordTeamId $TEAM_RVSF) [
        return $colorsRecordTimeRVSF;
      ]
    ]

  ][
    if ($highlightAsBestScoreTime) [
      return $colorsBestMapRecordScoreTime;
    ][
      return $colorsRecordTime;
    ]
  ]

]


// Private Functions

//
// Returns the color to prepend to a player name.
//
// @param int mapRecordTeamId ($arg1) The team id of the map record to which the player name belongs
//
// @return string The player name color
//
function getPlayerNameColor [

  if (= $optionShowMapRecordTeam 1) [
    return (getTeamColor $arg1);
  ][
    return (c $colorPlayerName);
  ]

]

//
// Returns the color to prepend to a weapon name.
//
// @param int mapRecordTeamId ($arg1) The team id of the map record to which the weapon name belongs
//
// @return string The weapon name color
//
function getWeaponNameColor [

  if (= $optionShowMapRecordTeam 3) [
    return (getTeamColor $arg1);
  ][
    return (c $colorWeapon);
  ]

]

//
// Returns the color to prepend to a timestamp date string.
//
// @param int teamId ($arg1) The team id
//
// @return string The timestamp date string color
//
function getTimestampDateColor [

  if (= $optionShowMapRecordTeam 4) [
    return (getTeamColor $arg1);
  ][
    return (c $colorMapRecordTimestampDate);
  ]

]

//
// Returns the color to prepend to a timestamp time string.
//
// @param int teamId ($arg1) The team id
//
// @return string The timestamp time string color
//
function getTimestampTimeColor [

  if (= $optionShowMapRecordTeam 4) [
    return (getTeamColor $arg1);
  ][
    return (c $colorMapRecordTimestampTime);
  ]

]

//
// Returns the color to use for a specified team.
//
// @param int teamId ($arg1) The team id
//
// @return string The team color
//
function getTeamColor [

  if (= $arg1 $TEAM_CLA) [
    return (c $colorTeamCLA);
  ][
    if (= $arg1 $TEAM_RVSF) [
      return (c $colorTeamRVSF);
    ]
  ]

]
