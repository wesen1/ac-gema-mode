//
// @author wesen
// @copyright 2017-2020 wesen <wesen-ac@web.de>
//

//
// Map record menu string generator module for ac-gema-mode
//
// Handles generating of menu strings to display map record information in menu items.
//
// Requires:
//   * scripts/ac-gema-mode/map-record/map-record-array.cfg
//   * scripts/ac-gema-mode/map-record/storage/best-map-record-cache.cfg
//   * scripts/ac-gema-mode/map-record/storage/map-record-loader.cfg
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/time/time-formatter.cfg
//   * scripts/ac-gema-mode/util/weapon-name-fetcher.cfg
//

persistidents 0;


// Public Functions

//
// Returns the menu string for the best map record in the main menu.
//
// @param string mapName ($arg1) The map name
//
// @return string The best map record menu string
//
const getBestMapRecordMenuString [

  mapName = $arg1;
  bestMapRecord = (getBestMapRecord $mapName);

  bestMapRecordMenuString = (getScoreTimeMenuString $bestMapRecord);

  if (! (strcmp $bestMapRecord "")) [
    weaponInfoString = (format $messageBestMapRecordWeaponMenu (getMapRecordWeaponName $bestMapRecord));
    bestMapRecordMenuString = (concatword $bestMapRecordMenuString $weaponInfoString);
  ]

  return $bestMapRecordMenuString;

]

//
// Returns the menu string for a record with a specific weapon in the "Weapon Records" menu.
//
// @param string mapName ($arg1) The map name
// @param int weaponId ($arg2) The weapon id
//
// @return string The weapon map record menu string
//
const getWeaponMapRecordMenuString [

  mapName = $arg1;
  bestMapRecord = (getBestMapRecord $mapName);

  weaponId = $arg2;

  mapRecord = (loadWeaponMapRecord $mapName $weaponId);
  if (&& (! (strcmp $mapRecord "")) (strcmp $mapRecord $bestMapRecord)) [
    menuStringTemplate = $bestWeaponMapRecordMenuText;
  ][
    menuStringTemplate = $weaponMapRecordMenuText;
  ]

  return (format $menuStringTemplate (getWeaponName $weaponId) (getScoreTimeMenuString $mapRecord));

]


// Private Functions

//
// Returns a string that represents a map record's score time for a menu.
// Will return a "No record" message when the map record is empty.
//
// @param array mapRecord ($arg1) The map record
//
// @return string The map record score time string
//
const getScoreTimeMenuString [

  mapRecord = $arg1;

  if (strcmp $mapRecord "") [
    return $messageNoMapRecord;
  ][
    scoreTimeString = (generateTimeStringFromMilliseconds (getMapRecordScoreTime $mapRecord) 1);
    return (format $messageMapRecordTime $scoreTimeString);
  ]

]
