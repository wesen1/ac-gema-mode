//
// @author wesen
// @copyright 2017-2019 wesen <wesen-ac@web.de>
//

//
// Map record menu string generator module for ac-gema-mode
//
// Handles generating of menu strings to display map record information in menuitems
//
// Requires:
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/map-record/storage/best-map-record-loader.cfg
//   * scripts/ac-gema-mode/map-record/storage/map-record.cfg
//   * scripts/ac-gema-mode/map-record/storage/map-record-storage.cfg
//   * scripts/ac-gema-mode/util/time-parser.cfg
//   * scripts/ac-gema-mode/util/weapon-name-fetcher.cfg
//


persistidents 0;

// Public Functions

//
// Returns the menu string for the best map record in the main menu.
//
// @param string $arg1 The map name
//
// @return string The map record menu string
//
const getBestMapRecordMenuString [

  mapName = $arg1;
  bestMapRecord = (getBestMapRecord $mapName);

  bestMapRecordMenuString = (getScoreTimeMenuString $bestMapRecord);

  if (! (strcmp $bestMapRecord "")) [
    weaponInfoString = (format $messageBestMapRecordWeaponMenu (getMapRecordWeaponName $bestMapRecord));
    bestMapRecordMenuString = (concatword $bestMapRecordMenuString $weaponInfoString);
  ]

  return $bestMapRecordMenuString;

]

//
// Returns the menu item text for a record with a specific weapon.
//
// @param string $arg1 The map name
// @param int $arg2 The weapon id
//
// @return string The map record menu string
//
const getWeaponMapRecordMenuString [

  mapName = $arg1;
  weaponId = $arg2;

  mapRecord = (loadMapRecord $mapName $weaponId);

  return (format $weaponMapRecordMenuText (getWeaponName $weaponId) (getScoreTimeMenuString $mapRecord));

]


// Private Functions

//
// Returns a string that represents a map records score time for a menu.
//
// @param array $arg1 The map record
//
// @return string The map record score time string
//
const getScoreTimeMenuString [

  mapRecord = $arg1;

  if (strcmp $mapRecord "") [
    return $messageNoMapRecord;
  ][
    scoreTimeString = (generateTimeString (getMapRecordScoreTime $mapRecord) 1);
    return (format $messageMapRecordTime $scoreTimeString);
  ]

]
