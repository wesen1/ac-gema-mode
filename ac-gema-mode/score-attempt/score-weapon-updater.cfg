//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

//
// Score weapon updater module for ac-gema-mode
//
// Provides functions to update the score weapon of a score attempt.
//
// Requires:
//   * scripts/ac-gema-mode/notification-printer.cfg
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/util/weapon-name-fetcher.cfg
//

// Options

//
// Option to show messages that the knife or pistol only mode were aborted because the player used a different weapon (Default: 0)
//
// @var int $optionShowSecondaryWeaponModeAbortedMessages
//
check2init optionShowSecondaryWeaponModeAbortedMessages 0;


persistidents 0;

//
// Checks whether a used weapon aborts knife only or pistol only mode and returns the new score weapon id.
// Will return -1 if the score weapon doesn't need to be changed
//
// @param int usedWeaponId ($arg1) The id of the weapon that the player used
// @param int currentScoreWeaponId ($arg2) The id of the current score weapon
//
// @return int The updated score weapon id
//
const getUpdatedScoreWeapon [

  usedWeaponId = $arg1;
  currentScoreWeaponId = $arg2;

  updatedScoreWeaponId = -1;
  if (&& (!= $usedWeaponId $KNIFE) (!= $usedWeaponId $GRENADES))[
    // The used weapon was neither knife nor grenade

    if (= $currentScoreWeaponId $KNIFE) [
      // The current score weapon is knife, the score weapon must be updated

      if (= $usedWeaponId $AKIMBO) [
        updatedScoreWeaponId = $PISTOL;
      ][
        updatedScoreWeaponId = $weaponId;
      ]

      if ($optionShowSecondaryWeaponModeAbortedMessages) [
        // Show knife only mode aborted message
	      printGemaNotification (format $messageSecondaryWeaponModeAborted (getWeaponName $KNIFE) (getWeaponName $updatedScoreWeaponId));
      ]

    ][
      if (&& (= $currentScoreWeaponId $PISTOL) (&& (!= $usedWeaponId $PISTOL) (!= $usedWeaponId $AKIMBO)))[
        // The current score weapon is pistol and something other than pistol and akimbo was used
        updatedScoreWeaponId = $usedWeaponId;

        if ($optionShowSecondaryWeaponModeAbortedMessages) [
          // Show pistol only mode aborted message
	        printGemaNotification (format $messageSecondaryWeaponModeAborted (getWeaponName $PISTOL) (getWeaponName $updatedScoreWeaponId));
	      ]

      ]
    ]

  ]

  return $updatedScoreWeaponId;
]
