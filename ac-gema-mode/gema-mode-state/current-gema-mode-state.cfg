//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Current gema mode state module for ac-gema-mode
//
// Provides functions to manage the current gema mode state.
//
// Requires:
//   * scripts/ac-gema-mode/gema-mode-state/environment-checker.cfg
//

// Global variables

//
// Stores whether the gema mode is currently enabled
//
// @var int $currentGemaModeState
//
check2init currentGemaModeState 0;


persistidents 0;

// Public Functions

//
// Returns the current gema mode state.
//
// @return int 1 if the gema mode is currently enabled, 0 otherwise
//
const getCurrentGemaModeState [
  return $currentGemaModeState;
]

//
// Sets the current gema mode state.
// The state is only changed if the new state differs from the current state.
// Also fires the "onGemaModeEnabled" and "onGemaModeDisabled" when the state changes accordingly.
//
// @param int newGemaModeState ($arg1) The state to which the gema mode state should be set (0 or 1)
//
// @return string The reason if the state was not changed to the new state or an empty string otherwise
//
// @example `setGemaModeState 1` Enables the gema mode if it is not already enabled
// @example `setGemaModeState 0` Disables the gema mode if it is not already disabled
//
const setCurrentGemaModeState [

  newGemaModeState = $arg1;
  gemaModeStateCanBeChanged = (!= $currentGemaModeState $newGemaModeState);

  if (&& $gemaModeStateCanBeChanged (&& (= $newGemaModeState 1) (! (isGema)))) [
    // The gema mode shall be activated and the environment is not gema compatible
    gemaModeStateNotChangedReason = (getEnvironmentNotGemaCompatibleReason);
    gemaModeStateCanBeChanged = 0;
  ][
    gemaModeStateNotChangedReason = "";
  ]

  if ($gemaModeStateCanBeChanged) [
    currentGemaModeState = $newGemaModeState;
    if (= $currentGemaModeState 1) [
      onGemaModeEnabled;
    ][
      onGemaModeDisabled;
    ]
  ]

  return $gemaModeStateNotChangedReason;

]


// Custom Events

//
// Event handler that is called when the gema mode state changes from "disabled" to "enabled".
//
checkinit onGemaModeEnabled [
]

//
// Event handler that is called when the gema mode state changes from "enabled" to "disabled".
//
checkinit onGemaModeDisabled [
]
