//
// @author wesen
// @copyright 2017-2020 wesen <wesen-ac@web.de>
//

//
// Map name checker module for ac-gema-mode
//
// Provides functions to check whether a map name is a gema map name.
//
// Requires:
//  * scripts/ac-gema-mode/util/string-utils.cfg
//

persistidents 0;

// Constants

//
// A list of implicit words that will cause a map name to be identified as a gema map name.
// The default implicits words are "jigsaw" and "deadmeat-10"
//
// @var string[] $gemaMapNameImplicits
//
const gemaMapNameImplicits ["jigsaw" "deadmeat-10"];

//
// The list of letters of which one per code position must occur in sequence to cause a map name to be
// identified as a gema map name.
//
// The default code letters are "g", "3e", "m" and "a@4"
//
// For example gema, g3ma or g3m@ would be identified as gema code.
//
// @var string[] $gemaMapNameCodes
//
const gemaMapNameCodes ["g" "3e" "m" "a@4"];


// Global variables

//
// The last checked map name of the isGemaMapName function
//
// @var string $cachedMapName
//
checkinit cachedMapName "";

//
// The result of the isGemaMapName call for the last checked map name.
// This is done to avoid checking one map name multiple times
//
// @var int $isCachedMapNameGemaMapName
//
checkinit isCachedMapNameGemaMapName 0;


// Public Functions

//
// Returns whether a map name is a gema map name.
//
// @param string mapName ($arg1) The map name
//
// @return int 1 if the map name is a gema map name, 0 otherwise
//
const isGemaMapName [

  if (! (strcmp $arg1 $cachedMapName)) [

    cachedMapName = $arg1;
    lowerCaseMapName = (tolower $arg1);
    isCachedMapNameGemaMapName = (|| (containsGemaMapNameImplicit $lowerCaseMapName) (containsGemaMapNameCodes $lowerCaseMapName))
  ]

  return $isCachedMapNameGemaMapName;

]


// Private Functions

//
// Returns whether a map name contains one of the implicit words.
//
// @param string mapName ($arg1) The map name
//
// @return int 1 if the map name contains one of the implicit words, 0 otherwise
//
// @example `containsGemaMapNameImplicit jigsaw-map-1` Will return 1 because "jigsaw-map-1" contains the implicit word "jigsaw"
// @example `containsGemaMapNameImplicit ac_douze` Will return 0 because "ac_douze" does not contain any of the implicit words
//
const containsGemaMapNameImplicit [
  return (strstr_array $arg1 $gemaMapNameImplicits);
]

//
// Returns whether a map name contains ge3ma@4.
//
// @param string mapName ($arg1) The map name
//
// @return int 1 if the map name contains ge3ma@4, 0 otherwise
//
// @example `containsGemaMapNameCodes gema_warm_up` Will return 1 because "gema_warm_up" contains the word "gema"
// @example `containsGemaMapNameCodes ac_edifice` Will return 0 because "ac_edifice" does not contain ge3ma@4
//
const containsGemaMapNameCodes [

  checkMapName = $arg1;
  gemaMapNameCodePosition = 0;
  numberOfGemaMapNameCodes = (listlen $gemaMapNameCodes);

  // Iterate over all letters of the map name
  parsestring $checkMapName mapNameLetter [

    if (letterMatchesGemaMapNameCode $mapNameLetter $gemaMapNameCodePosition) [
      ++ gemaMapNameCodePosition;
      if (= $gemaMapNameCodePosition $numberOfGemaMapNameCodes) [
        breakparse;
      ]
    ][
      lastGemaMapNameCodePosition = $gemaMapNameCodePosition;
      gemaMapNameCodePosition = 0;

      if (> $lastGemaMapNameCodePosition 0) [
        if (letterMatchesGemaMapNameCode $mapNameLetter 0) [
          gemaMapNameCodePosition = 1;
        ]
      ]
    ]
  ]

  return (= $gemaMapNameCodePosition $numberOfGemaMapNameCodes);

]

//
// Returns whether a letter matches a specific position of the gema map name code.
//
// @param string letter ($arg1) The letter
// @param int codePosition ($arg2) The gema map name code position
//
// @return int 1 if the letter matches the gema map name code position, 0 otherwise
//
// @example `letterMatchesGemaMapNameCode a 2` Will return 0 because "a" does not match "m"
// @example `letterMatchesGemaMapNameCode a 3` Will return 1 because "a" matches "a@4"
//
const letterMatchesGemaMapNameCode [
  checkLetter = $arg1;
  gemaMapNameCode = (at $gemaMapNameCodes $arg2);

  return (strstr $gemaMapNameCode $checkLetter);
]
