//
// @author wesen
// @copyright 2017-2020 wesen <wesen-ac@web.de>
//

//
// Environment checker module for ac-gema-mode
//
// Provides functions to check whether the environment (game mode and map) is gema compatible.
//
// Requires:
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/gema-mode-state/map-name-checker.cfg
//

persistidents 0;

// Public Functions

//
// Returns whether the current environment is gema compatible.
// This takes into account the game mode, map name and whether a demo is played back at the moment.
//
// The environment is gema compatible if the game mode is "CTF", the map name is a gema map name and no demo
// is played back at the moment.
//
// @return int 1 if the environment is gema compatible, 0 otherwise
//
const isGema [
  return (strcmp (getEnvironmentNotGemaCompatibleReason) "");
]

//
// Returns the reason why the current environment is not gema compatible.
//
// @return string The reason or an empty string if the environment is gema compatible
//
const getEnvironmentNotGemaCompatibleReason [

  if (! (strcmp (getmode 1) "CTF")) [
    return $messageGameModeNotCtf;
  ][
    if (! (isGemaMapName (curmap))) [
      return $messageMapNotGemaMap;
    ][
      if (watchingdemo) [
        return $messageWatchingDemo;
      ]
    ]
  ]

]
