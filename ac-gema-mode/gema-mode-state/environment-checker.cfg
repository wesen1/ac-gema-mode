//
// @author wesen
// @copyright 2017-2019 wesen <wesen-ac@web.de>
//

exec "scripts/ac-gema-mode/gema-mode-state/map-name-checker.cfg";

//
// Environment checker module for ac-gema-mode
// Provides methods to check whether the environment (game mode and map) are gema compatible.
//
// Requires:
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/gema-mode-state/map-name-checker.cfg
//

persistidents 0;

// Public Functions

//
// Returns whether the current environment is gema compatible (CTF and gema map name).
//
// @return int 1 if the environment is gema compatible, 0 otherwise
//
const isGema [
  return (strcmp (getEnvironmentNotGemaCompatibleReason) "");
]

//
// Returns the reason why an environment is not gema compatible.
//
// @return string The reason or an empty string if the environment is gema compatible
//
const getEnvironmentNotGemaCompatibleReason [

  if (! (strcmp (getmode) "ctf")) [
    return $messageGameModeNotCtf;
  ][
    if (! (isGemaMapName (curmap))) [
      return $messageMapNotGemaMap;
    ][
      if (watchingdemo) [
        return $messageWatchingDemo;
      ]
    ]
  ]

]
