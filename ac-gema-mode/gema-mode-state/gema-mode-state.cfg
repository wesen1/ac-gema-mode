//
// @author wesen
// @copyright 2017-2020 wesen <wesen-ac@web.de>
//

//
// Gema mode state module for ac-gema-mode
//
// Manages enabling and disabling the current gema mode state based on various events.
//
// Requires:
//   * scripts/ac-gema-mode/custom-declarations.cfg
//   * scripts/ac-gema-mode/gema-mode-state/current-gema-mode-state.cfg
//   * scripts/ac-gema-mode/gema-mode-state/environment-checker.cfg
//   * scripts/ac-gema-mode/notification-printer.cfg
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/unpersistent/declarations.cfg
//

// Options

//
// Option to automatically activate the gema mode on gema maps (Default: 1)
//
// @var int $optionAutoActivateGemaMode
//
option optionAutoActivateGemaMode 1;


persistidents 0;

// Public Functions

//
// Returns whether the gema mode is currently active.
//
// @return int 1 if the gema mode is active, 0 otherwise
//
function isGemaModeActive [
  return (&& (= (getCurrentGemaModeState) 1) (isGema));
]


//
// Automatically enables the gema mode if required.
// This will only be done if $optionAutoActivateGemaMode is set to 1.
//
function autoEnableGemaModeIfRequired [
  if (&& $optionAutoActivateGemaMode (= (getCurrentGemaModeState) 0)) [
    tryToSetCurrentGemaModeState 1 $messageGemaModeAutoEnabled;
  ]
]

//
// Automatically disables the gema mode if required.
//
function autoDisableGemaModeIfRequired [
  if (= (getCurrentGemaModeState) 1) [
    tryToSetCurrentGemaModeState 0 (format $messageGemaModeAutoDisabled (getEnvironmentNotGemaCompatibleReason));
  ]
]


// Custom Events

//
// Event handler that is called after the gema mode state auto change checks on map start are complete.
//
// @param int newGemaModeState ($arg1) The new gema mode state
//
event onMapStartGemaModeStateAutoChangeChecksFinished [
]


// Event Handlers

//
// Event handler that is called when a new map starts.
//
eventlistener mapstartalways [

  if (isGema) [
    autoEnableGemaModeIfRequired;
  ][
    autoDisableGemaModeIfRequired;
  ]

  onMapStartGemaModeStateAutoChangeChecksFinished (getCurrentGemaModeState);

]

//
// Event handler that is called when the player uses /newmap.
//
eventlistener onNewMap [
  tryToSetCurrentGemaModeState 0;
]

//
// Event handler that is called when the game is closed.
//
eventlistener onQuit [
  tryToSetCurrentGemaModeState 0;
]
