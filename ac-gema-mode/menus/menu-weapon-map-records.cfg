//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

//
// Configuration for the "Weapon Records" menu
//
// Requires:
//   * scripts/ac-gema-mode/global-constants.cfg
//   * scripts/ac-gema-mode/map-record/output/menus/menu-string-generator.cfg
//   * scripts/ac-gema-mode/menus/menu-delete-record.cfg
//   * scripts/ac-gema-mode/strings.cfg
//

persistidents 0;


// Public Functions

//
// Hides the score weapon model above the menu.
//
const hideScoreWeaponModel [
  chmenumdl $menuShowRecordsTitle;
]

//
// Generates the weapon map record menu items for all weapons.
//
const generateWeaponMapRecordMenuItems [

  // Primary Weapons
  loop weaponIndex (listlen $primaryWeaponIds) [
    weaponId = (at $primaryWeaponIds $weaponIndex);
    modelDirectoryName = (at $DEFAULT_PRMDLDIRS $weaponIndex)

    generateWeaponMapRecordMenuItem $weaponId (concatword $modelDirectoryName "/menu");
  ]

  // Secondary Weapons
  generateWeaponMapRecordMenuItem $PISTOL;
  generateWeaponMapRecordMenuItem $KNIFE;

]


// Private Functions

//
// Generates a weapon map record menu item for a specific weapon.
//
// @param int weaponId ($arg1) The weapon id
// @param string weaponModelDirectoryPath ($arg2) The path to the weapon model to display above the "Weapon Records" menu (optional)
//
const generateWeaponMapRecordMenuItem [

  weaponId = $arg1;
  modelDirectoryPath = $arg2;

  menuItemText = (format "getWeaponMapRecordMenuString (curmap) %1" $weaponId);
  menuItemSelectAction = (format "showDeleteRecordMenu (curmap) %1" $weaponId);

  if (strcmp $modelDirectoryPath "") [
    // No weapon model was specified
    menuItemHoverAction = "hideScoreWeaponModel";
  ][
    // A weapon model was specified, show it above the menu
    menuItemHoverAction = (format "chmenumdl $menuShowRecordsTitle weapons/%1 mapmodel 75 12" $modelDirectoryPath);
  ]

  execute (format "menuitemvar [ %1 ][ %2 ][ %3 ]" $menuItemText $menuItemSelectAction $menuItemHoverAction);

]


// Event Handlers

//
// Event handler that is called when a new map starts.
//
checkinit mapstartalways [
  // Select the map record with Assault Rifle
  menuselection $menuShowRecordsTitle 2;
]


// Menu configuration

newmenu $menuShowRecordsTitle;

menuitemvar [ format $menuItemCurrentMap (curmap) ];
menuitem [][][ hideScoreWeaponModel ];
generateWeaponMapRecordMenuItems;
menuitem [][][ hideScoreWeaponModel ];
menuitem $menuItemGoBackText [ closemenu $menuShowRecordsTitle ];
