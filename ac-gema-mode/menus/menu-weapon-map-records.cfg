//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

//
// Configuration for the "Weapon Records" menu
//
// Requires:
//   * scripts/ac-gema-mode/global-constants.cfg
//   * scripts/ac-gema-mode/map-record/output/menus/menu-string-generator.cfg
//   * scripts/ac-gema-mode/menus/menu-delete-record.cfg
//   * scripts/ac-gema-mode/menus/menu-migrate-records.cfg
//   * scripts/ac-gema-mode/output/tab-stop-calculator.cfg
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/util/list-utils.cfg
//

persistidents 0;


// Global Variables

//
// The last selected value of the map revision slider menu item
// This is the index of the corresponding map revision inside the list of map revisions
//
// @var int $lastSelectedRevisionSliderValue
//
checkinit lastSelectedRevisionSliderValue 0;

//
// The last selected map revision of the map revision slider menu item
//
// @var int $lastSelectedMapRevision
//
checkinit lastSelectedMapRevision $curmaprevision;

//
// The last calculated numbers of pad tabs for the weapon record item texts
//
// @var int[] $lastCalculatedMenuPadTabsForWeaponRecordsMenuWeaponRecordItems
//
checkinit lastCalculatedMenuPadTabsForWeaponRecordsMenuWeaponRecordItems [];

//
// The last calculated numbers of pad tabs for the map info item texts
//
// @var int[] $lastCalculatedMenuPadTabsForWeaponRecordsMenuMapInfoItems
//
checkinit lastCalculatedMenuPadTabsForWeaponRecordsMenuMapInfoItems [];


// Public Functions

//
// Calculates the menu pad tabs if required and returns the last calculated numbers of pad tabs for the weapon record item texts.
//
// @return int[] The last calculated numbers of pad tabs for the weapon record item texts
//
const getMenuPadTabsForWeaponRecordsMenuWeaponRecordItems [
  calculateMenuPadTabs $__fontsetting;
  return $lastCalculatedMenuPadTabsForWeaponRecordsMenuWeaponRecordItems;
]

//
// Calculates the menu pad tabs if required and returns the last calculated numbers of pad tabs for the map info item texts.
//
// @return int[] The last calculated numbers of pad tabs for the map info item texts
//
const getMenuPadTabsForWeaponRecordsMenuMapInfoItems [
  calculateMenuPadTabs $__fontsetting;
  return $lastCalculatedMenuPadTabsForWeaponRecordsMenuMapInfoItems;
]

//
// Calculates the menu pad tabs for the weapon record item texts.
//
// @param int fontType ($arg1) The font type to calculate the pad tabs for
//
const calculateMenuPadTabsForWeaponRecordsMenuWeaponRecordItems [

  fontType = $arg1;

  tabPaddedWeaponRecordsMenuWeaponRecordTexts = [];
  looplist $allWeaponIds weaponId [
    add2list tabPaddedWeaponRecordsMenuWeaponRecordTexts (strreplace (getWeaponMapRecordStringTitle $weaponId) " " $TEMPORARY_WHITESPACE);
  ]

  lastCalculatedMenuPadTabsForWeaponRecordsMenuWeaponRecordItems = (calculateDistancesToClosestCommonNextTabStopNumber $tabPaddedWeaponRecordsMenuWeaponRecordTexts 0 $fontType);

]

//
// Calculates the menu pad tabs for the map info item texts.
//
// @param int fontType ($arg1) The font type to calculate the pad tabs for
//
const calculateMenuPadTabsForWeaponRecordsMenuMapInfoItems [

  fontType = $arg1;

  tabPaddedWeaponRecordsMenuMapInfoTexts = [];
  add2list tabPaddedWeaponRecordsMenuMapInfoTexts (strreplace $menuItemCurrentMapNameTitle " " $TEMPORARY_WHITESPACE);
  add2list tabPaddedWeaponRecordsMenuMapInfoTexts (strreplace $menuItemCurrentMapRevisionSliderTitle " " $TEMPORARY_WHITESPACE);

  previousPadTabsForMapInfoItems = $lastCalculatedMenuPadTabsForWeaponRecordsMenuMapInfoItems;
  lastCalculatedMenuPadTabsForWeaponRecordsMenuMapInfoItems = (calculateDistancesToClosestCommonNextTabStopNumber $tabPaddedWeaponRecordsMenuMapInfoTexts 0 $fontType);

  previousRevisionSliderPadTabs = (at $previousPadTabsForMapInfoItems 1);
  newRevisionSliderPadTabs = (at $lastCalculatedMenuPadTabsForWeaponRecordsMenuMapInfoItems 1);

  if (&& (> (getRevisionSliderMaxValue) 0) (!= $previousRevisionSliderPadTabs $newRevisionSliderPadTabs))  [
    // The ShowRecords menu needs to be rebuilt because the revision slider text is not dynamic
    rebuildShowRecordsMenuOnNextShow = 1;
  ]

]


//
// Hides the score weapon model above the menu.
//
const hideScoreWeaponModel [
  chmenumdl $menuShowRecordsTitle;
]

//
// Generates the weapon map record menu items for all weapons.
//
const generateWeaponMapRecordMenuItems [

  // Primary Weapons
  loop weaponIndex (listlen $primaryWeaponIds) [
    weaponId = (at $primaryWeaponIds $weaponIndex);
    modelDirectoryName = (at $DEFAULT_PRMDLDIRS $weaponIndex);

    generateWeaponMapRecordMenuItem $weaponId (concatword $modelDirectoryName "/menu");
  ]

  // Secondary Weapons
  generateWeaponMapRecordMenuItem $PISTOL;
  generateWeaponMapRecordMenuItem $KNIFE;

]


//
// Fetches and returns the values for the map revision slider.
//
// @return int[] The map revisions for the map revision slider
//
const getRevisionSliderValues [

  storedMapRevisions = (getAllStoredMapRevisions (curmap));
  if (strcmp $storedMapRevisions "") [
    // No stored map revisions, seems to be a non gema map
    return $curmaprevision;
  ][
    return $storedMapRevisions;
  ]

]

//
// Returns the maximum value for the map revision slider.
//
// @return int The maximum value for the map revision slider
//
const getRevisionSliderMaxValue [
  return (- (listlen (getRevisionSliderValues)) 1);
]

//
// Handler that is called when a value is selected via the map revision slider.
//
// @param int revisionSliderValue ($arg1) The new revision slider value
//
const onRevisionSliderValueSelected [

  newSelectedRevisionSliderValue = $arg1;
  if (!= $lastSelectedRevisionSliderValue $newSelectedRevisionSliderValue) [

    lastSelectedRevisionSliderValue = $newSelectedRevisionSliderValue;
    lastSelectedMapRevision = (at (getRevisionSliderValues) $lastSelectedRevisionSliderValue);

    // The menu needs to be refreshed to reload the weapon map records and to update the "Migrate Records" menu item
    refreshMenu $menuShowRecordsTitle 1;
  ]

]


// Private Functions

//
// Returns the text for the "Map: <map name>" menu item.
//
// @return string The text for the "Map: <map name>" menu item
//
const getMenuItemCurrentMapNameText [
  numberOfPadTabs = (at (getMenuPadTabsForWeaponRecordsMenuMapInfoItems) 0);
  return (format $menuItemCurrentMapName (strrepeat $TAB_CHARACTER $numberOfPadTabs) (curmap));
]

//
// Returns the text for the "Revision: <slider>" menu item.
//
// @return string The text for the "Revision: <slider>" menu item
//
const getMenuItemCurrentMapRevisionSliderText [
  numberOfPadTabs = (at (getMenuPadTabsForWeaponRecordsMenuMapInfoItems) 1);
  return (format $menuItemCurrentMapRevisionSliderText (strrepeat $TAB_CHARACTER $numberOfPadTabs));
]

//
// Generates a weapon map record menu item for a specific weapon.
//
// @param int weaponId ($arg1) The weapon id
// @param string weaponModelDirectoryPath ($arg2) The path to the weapon model to display above the "Weapon Records" menu (optional)
//
const generateWeaponMapRecordMenuItem [

  weaponId = $arg1;
  modelDirectoryPath = $arg2;

  weaponIndex = (findlist $allWeaponIds $weaponId);

  menuItemText = (format "getWeaponMapRecordMenuString %1 %2 (at (getMenuPadTabsForWeaponRecordsMenuWeaponRecordItems) %3) $lastSelectedMapRevision" (curmap) $weaponId $weaponIndex);
  menuItemSelectAction = (format "showDeleteRecordDialog %1 $lastSelectedMapRevision %2" (curmap) $weaponId);

  if (strcmp $modelDirectoryPath "") [
    // No weapon model was specified
    menuItemHoverAction = "hideScoreWeaponModel";
  ][
    // A weapon model was specified, show it above the menu
    menuItemHoverAction = (format "chmenumdl $menuShowRecordsTitle weapons/%1 mapmodel 75 12" $modelDirectoryPath);
  ]

  execute (format "menuitemvar [ %1 ][ %2 ][ %3 ]" $menuItemText $menuItemSelectAction $menuItemHoverAction);

]

//
// Generates and returns the text for the "Migrate Records" menu item.
//
// @return string The text for the "Migrate Records" menu item
//
const getMigrateRecordsMenuItemText [

  if (= $lastSelectedMapRevision (getCurrentMapRevision (curmap))) [
    // The current map revision is the selected map revision
    return $menuItemMigrateRecordsFromOtherRevisions;
  ][
    return $menuItemMigrateRecordsToCurrentRevision;
  ]

]

//
// Handler that is called when the "Migrate Records" menu item is selected.
// Configures and shows the "Migrate Records" dialog.
//
const onMigrateRecordsSelected [

  if (= $lastSelectedMapRevision (getCurrentMapRevision (curmap))) [
    showMigrateRecordsDialog (curmap) -1;
  ][
    showMigrateRecordsDialog (curmap) $lastSelectedMapRevision;
  ]

]


// Event Handlers

// Event handler that is called before the "Show Records" menu is shown.
// Rebuilds the menu if required.
//
const onBeforeShowRecordsMenuShow [

  if ($rebuildShowRecordsMenuOnNextShow) [
    closemenu $menuShowRecordsTitle;

    rebuildShowRecordsMenu;
    rebuildShowRecordsMenuOnNextShow = 0;

    showmenu $menuShowRecordsTitle;
  ]

]

//
// Event handler that is called when a new map starts.
//
checkinit mapstartalways [
  // The "Show records" menu must be rebuilt to update the map revision slider
  rebuildShowRecordsMenuOnNextShow = 1;
]

//
// Event handler that is called when the map revisions with records for a map were updated.
//
// @param string mapName ($arg1) The map name
// @param int[] oldMapRevisions ($arg2) The old list of map revisions
// @param int[] newMapRevisions ($arg3) The new list of map revisions
//
checkinit onMapRevisionsWithRecordsUpdated [
  if (strcmp $arg1 (curmap)) [
    rebuildShowRecordsMenuOnNextShow = 1;
  ]
]


// Menu configuration

addMenuPadTabCalculator calculateMenuPadTabsForWeaponRecordsMenuWeaponRecordItems;
addMenuPadTabCalculator calculateMenuPadTabsForWeaponRecordsMenuMapInfoItems;

//
// Builds the "Show records" menu.
//
const buildShowRecordsMenu [

  // Set up the map revision slider variables
  lastSelectedMapRevision = $curmaprevision;
  lastSelectedRevisionSliderValue = (findlist (getRevisionSliderValues) $lastSelectedMapRevision);


  newmenu $menuShowRecordsTitle;
  menuinit [ onBeforeShowRecordsMenuShow ]

  if (= (getRevisionSliderMaxValue) 0) [
    // There is only the current map revision, no need to display a slider
    menuitem (format $menuItemCurrentMap "" (curmap) $curmaprevision) [ showDeleteRecordDialog (curmap) -1 -1 ];
  ][
    // There are multiple map revisions to chose from, display a slider
    menuitemvar [ getMenuItemCurrentMapNameText ][ showDeleteRecordDialog (curmap) -1 -1 ];
    menuitemslider (getMenuItemCurrentMapRevisionSliderText) 0 (getRevisionSliderMaxValue) [ $lastSelectedRevisionSliderValue ] 1 (getRevisionSliderValues) [ onRevisionSliderValueSelected ];
  ]

  menuitem [][][ hideScoreWeaponModel ];
  generateWeaponMapRecordMenuItems;
  menuitem [][][ hideScoreWeaponModel ];

  if (> (getRevisionSliderMaxValue) 0) [
    // There is more than one map revision
    menuitemvar [ getMigrateRecordsMenuItemText ][ onMigrateRecordsSelected ];
    menuitem $menuItemDeleteRecordsForRevision [ showDeleteRecordDialog (curmap) $lastSelectedMapRevision -1 ];
    menuitem;
  ]

  menuitem $menuItemGoBackText [ closemenu $menuShowRecordsTitle ];


  // Select the map record with Assault Rifle
  if (= (getRevisionSliderMaxValue) 0) [
    menuselection $menuShowRecordsTitle 2;
  ][
    menuselection $menuShowRecordsTitle 3;
  ]

]

//
// Deletes and rebuilds the "Show records" menu.
//
const rebuildShowRecordsMenu [
  delmenu $menuShowRecordsTitle;
  buildShowRecordsMenu;
]


buildShowRecordsMenu;
