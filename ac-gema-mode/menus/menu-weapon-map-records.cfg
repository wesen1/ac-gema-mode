//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

//
// Configuration for the "Weapon Records" menu
//
// Requires:
//   * scripts/ac-gema-mode/custom-declarations.cfg
//   * scripts/ac-gema-mode/global-constants.cfg
//   * scripts/ac-gema-mode/map-record/output/menus/menu-string-generator.cfg
//   * scripts/ac-gema-mode/menus/menu-delete-record.cfg
//   * scripts/ac-gema-mode/menus/menu-migrate-records.cfg
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//

persistidents 0;


// Global Variables

//
// The last selected value of the map revision slider menu item
// This is the index of the corresponding map revision inside the list of map revisions
//
// @var int $lastSelectedRevisionSliderValue
//
cache lastSelectedRevisionSliderValue 0;

//
// The last selected map revision of the map revision slider menu item
//
// @var int $lastSelectedMapRevision
//
cache lastSelectedMapRevision $curmaprevision;


// Public Functions

//
// Hides the score weapon model above the menu.
//
function hideScoreWeaponModel [
  chmenumdl $menuShowRecordsTitle;
]

//
// Generates the weapon map record menu items for all weapons.
//
function generateWeaponMapRecordMenuItems [

  // Primary Weapons
  loop weaponIndex (listlen $primaryWeaponIds) [
    weaponId = (at $primaryWeaponIds $weaponIndex);
    modelDirectoryName = (at $DEFAULT_PRMDLDIRS $weaponIndex);

    generateWeaponMapRecordMenuItem $weaponId (concatword $modelDirectoryName "/menu");
  ]

  // Secondary Weapons
  generateWeaponMapRecordMenuItem $PISTOL;
  generateWeaponMapRecordMenuItem $KNIFE;

]


//
// Fetches and returns the values for the map revision slider.
//
// @return int[] The map revisions for the map revision slider
//
function getRevisionSliderValues [

  storedMapRevisions = (getAllStoredMapRevisions (curmap));
  if (strcmp $storedMapRevisions "") [
    // No stored map revisions, seems to be a non gema map
    return $curmaprevision;
  ][
    return $storedMapRevisions;
  ]

]

//
// Returns the maximum value for the map revision slider.
//
// @return int The maximum value for the map revision slider
//
function getRevisionSliderMaxValue [
  return (- (listlen (getRevisionSliderValues)) 1);
]

//
// Handler that is called when a value is selected via the map revision slider.
//
// @param int revisionSliderValue ($arg1) The new revision slider value
//
function onRevisionSliderValueSelected [

  newSelectedRevisionSliderValue = $arg1;
  if (!= $lastSelectedRevisionSliderValue $newSelectedRevisionSliderValue) [

    lastSelectedRevisionSliderValue = $newSelectedRevisionSliderValue;
    lastSelectedMapRevision = (at (getRevisionSliderValues) $lastSelectedRevisionSliderValue);

    // The menu needs to be refreshed to reload the weapon map records and to update the "Migrate Records" menu item
    refreshMenu $menuShowRecordsTitle 1;
  ]

]


// Private Functions

//
// Generates a weapon map record menu item for a specific weapon.
//
// @param int weaponId ($arg1) The weapon id
// @param string weaponModelDirectoryPath ($arg2) The path to the weapon model to display above the "Weapon Records" menu (optional)
//
function generateWeaponMapRecordMenuItem [

  weaponId = $arg1;
  modelDirectoryPath = $arg2;

  menuItemText = (format "getWeaponMapRecordMenuString %1 %2 $lastSelectedMapRevision" (curmap) $weaponId);
  menuItemSelectAction = (format "showDeleteRecordDialog %1 $lastSelectedMapRevision %2" (curmap) $weaponId);

  if (strcmp $modelDirectoryPath "") [
    // No weapon model was specified
    menuItemHoverAction = "hideScoreWeaponModel";
  ][
    // A weapon model was specified, show it above the menu
    menuItemHoverAction = (format "chmenumdl $menuShowRecordsTitle weapons/%1 mapmodel 75 12" $modelDirectoryPath);
  ]

  execute (format "menuitemvar [ %1 ][ %2 ][ %3 ]" $menuItemText $menuItemSelectAction $menuItemHoverAction);

]

//
// Generates and returns the text for the "Migrate Records" menu item.
//
// @return string The text for the "Migrate Records" menu item
//
function getMigrateRecordsMenuItemText [

  if (= $lastSelectedMapRevision (getCurrentMapRevision (curmap))) [
    // The current map revision is the selected map revision
    return $menuItemMigrateRecordsFromOtherRevisions;
  ][
    return $menuItemMigrateRecordsToCurrentRevision;
  ]

]

//
// Handler that is called when the "Migrate Records" menu item is selected.
// Configures and shows the "Migrate Records" dialog.
//
function onMigrateRecordsSelected [

  if (= $lastSelectedMapRevision (getCurrentMapRevision (curmap))) [
    showMigrateRecordsDialog (curmap) -1;
  ][
    showMigrateRecordsDialog (curmap) $lastSelectedMapRevision;
  ]

]


// Event Handlers

//
// Event handler that is called before the "Show Records" menu is shown.
// Rebuilds the menu if required.
//
function onBeforeShowRecordsMenuShow [

  if ($rebuildShowRecordsMenuOnNextShow) [
    closemenu $menuShowRecordsTitle;

    rebuildShowRecordsMenu;
    rebuildShowRecordsMenuOnNextShow = 0;

    showmenu $menuShowRecordsTitle;
  ]

]

//
// Event handler that is called when a new map starts.
//
eventlistener mapstartalways [
  // The "Show records" menu must be rebuilt to update the map revision slider
  rebuildShowRecordsMenuOnNextShow = 1;
]

//
// Event handler that is called when the map revisions with records for a map were updated.
//
// @param string mapName ($arg1) The map name
// @param int[] oldMapRevisions ($arg2) The old list of map revisions
// @param int[] newMapRevisions ($arg3) The new list of map revisions
//
eventlistener onMapRevisionsWithRecordsUpdated [
  if (strcmp $arg1 (curmap)) [
    rebuildShowRecordsMenuOnNextShow = 1;
  ]
]


// Menu configuration

//
// Builds the "Show records" menu.
//
function buildShowRecordsMenu [

  // Set up the map revision slider variables
  lastSelectedMapRevision = $curmaprevision;
  lastSelectedRevisionSliderValue = (findlist (getRevisionSliderValues) $lastSelectedMapRevision);


  newmenu $menuShowRecordsTitle;
  menuinit [ onBeforeShowRecordsMenuShow ]

  if (= (getRevisionSliderMaxValue) 0) [
    // There is only the current map revision, no need to display a slider
    menuitem (format $menuItemCurrentMap (curmap) $curmaprevision) [ showDeleteRecordDialog (curmap) -1 -1 ];
  ][
    // There are multiple map revisions to chose from, display a slider
    menuitem (format $menuItemCurrentMapName (curmap)) [ showDeleteRecordDialog (curmap) -1 -1 ];
    menuitemslider $menuItemCurrentMapRevisionSliderTitle 0 (getRevisionSliderMaxValue) [ $lastSelectedRevisionSliderValue ] 1 (getRevisionSliderValues) [ onRevisionSliderValueSelected ];
  ]

  menuitem [][][ hideScoreWeaponModel ];
  generateWeaponMapRecordMenuItems;
  menuitem [][][ hideScoreWeaponModel ];

  if (> (getRevisionSliderMaxValue) 0) [
    // There is more than one map revision
    menuitemvar [ getMigrateRecordsMenuItemText ][ onMigrateRecordsSelected ];
    menuitem $menuItemDeleteRecordsForRevision [ showDeleteRecordDialog (curmap) $lastSelectedMapRevision -1 ];
    menuitem;
  ]

  menuitem $menuItemGoBackText [ closemenu $menuShowRecordsTitle ];


  // Select the map record with Assault Rifle
  if (= (getRevisionSliderMaxValue) 0) [
    menuselection $menuShowRecordsTitle 2;
  ][
    menuselection $menuShowRecordsTitle 3;
  ]

]

//
// Deletes and rebuilds the "Show records" menu.
//
function rebuildShowRecordsMenu [
  delmenu $menuShowRecordsTitle;
  buildShowRecordsMenu;
]


buildShowRecordsMenu;
