//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

persistidents 0;

exec "scripts/ac-gema-mode/menus/menu-delete-record.cfg";
exec "scripts/ac-gema-mode/menus/menu-maps.cfg";
exec "scripts/ac-gema-mode/menus/menu-weapon-map-records.cfg";
exec "scripts/ac-gema-mode/menus/menu-time-target.cfg";
exec "scripts/ac-gema-mode/menus/menu-options.cfg";

//
// Configuration for the main menu
//
// Requires:
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/gema-mode-state/gema-mode-state.cfg
//   * scripts/ac-gema-mode/map-record/output/menus/menu-string-generator.cfg
//   * scripts/ac-gema-mode/map-record/storage/best-map-record-cache.cfg
//   * scripts/ac-gema-mode/menus/menu-delete-record.cfg
//   * scripts/ac-gema-mode/menus/menu-maps.cfg
//   * scripts/ac-gema-mode/menus/menu-weapon-map-records.cfg
//   * scripts/ac-gema-mode/menus/menu-time-target.cfg
//   * scripts/ac-gema-mode/menus/menu-options.cfg
//   * scripts/ac-gema-mode/time/time-formatter.cfg
//   * scripts/ac-gema-mode/util/menu-utils.cfg
//


// Public Functions

//
// Shows either the "Weapon Records" or "Delete Records" menu depending on $optionEnableWeaponRecordsMenu.
// This function is called when the best map record menu item is selected by the user.
//
const menuItemBestMapRecordSelectAction [

  if ($optionEnableWeaponRecordsMenu) [
    showmenu $menuShowRecordsTitle;
  ][
    if (! (strcmp (getBestMapRecord (curmap)) "")) [
      // There is at least one record, show the "delete all records" menu
      showmenu $menuDeleteRecordTitle;
    ][
      // Do nothing
      -1;
    ]
  ]

]

//
// Function that is called when the "Enable Gema Mode" checkbox is toggled.
// Tries to set the gema mode state to the value of the checkbox and refreshes the menu if it fails.
//
// @param int checkBoxState ($arg1) The current state of the "Enable Gema Mode" checkbox
//
const onEnableGemaModeCheckBoxToggled [
  gemaModeStateWasSet = (tryToSetCurrentGemaModeState $arg1);
  if (! $gemaModeStateWasSet) [
    // The state was not set but the checkbox state changed, refresh the menu to undo the checkbox change
    refreshMenu $menuGemaModeTitle 0;
  ]
]

//
// Shows the "Gema Mode" menu.
//
const showGemaModeMenu [
  showmenu $menuGemaModeTitle;
]


newmenu $menuGemaModeTitle
menuitemcheckbox $menuItemEnableGemaModeText [ getCurrentGemaModeState ][ onEnableGemaModeCheckBoxToggled ]
menuitem
menuitemvar [ format $menuItemCurrentMap (curmap) ][ showmenu $menuMapsTitle ]
menuitemvar [ format $menuItemBestMapRecord (getBestMapRecordMenuString (curmap)) ][ menuItemBestMapRecordSelectAction ]
menuitemvar [ format $menuItemCurrentTimeTarget (generateTimeStringFromTimeInterval $timeTarget 1) ][ showmenu $menuTimeTargetTitle ]
menuitem
menuitem $menuItemOptions [ showmenu $menuOptionsTitle ]
menuitem
menuitem $menuItemCloseMenu [ closemenu $menuGemaModeTitle ]
