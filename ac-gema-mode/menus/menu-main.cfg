//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

//
// Configuration for the main menu
//
// Requires:
//   * scripts/ac-gema-mode/colors.cfg
//   * scripts/ac-gema-mode/gema-mode-state/gema-mode-state.cfg
//   * scripts/ac-gema-mode/map-record/output/menus/menu-string-generator.cfg
//   * scripts/ac-gema-mode/map-record/storage/best-map-record-cache.cfg
//   * scripts/ac-gema-mode/menus/menu-delete-record.cfg
//   * scripts/ac-gema-mode/menus/menu-maps.cfg
//   * scripts/ac-gema-mode/menus/menu-options.cfg
//   * scripts/ac-gema-mode/menus/menu-time-target.cfg
//   * scripts/ac-gema-mode/menus/menu-weapon-map-records.cfg
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/time/time-formatter.cfg
//   * scripts/ac-gema-mode/util/menu-utils.cfg
//

// Options

//
// Option to enable the "Weapon Records" menu (Default: 1).
// If set to 0 selecting the best map record will result in a "Delete records" dialog
// that affects all map records of the current map at once
//
// @var int $optionEnableWeaponRecordsMenu
//
check2init optionEnableWeaponRecordsMenu 1;

persistidents 0;


// Public Functions

//
// Shows either the "Weapon Records" or "Delete Records" menu depending on $optionEnableWeaponRecordsMenu.
// This function is called when the best map record menu item is selected.
//
const onMenuItemBestMapRecordSelected [

  if ($optionEnableWeaponRecordsMenu) [
    showmenu $menuShowRecordsTitle;
  ][
    if (strcmp (getBestMapRecord (curmap) $curmaprevision) "") [
      // There is no record for the current map, do nothing
      -1;
    ][
      // There is at least one map record for the current map, show the "Delete all records" menu
      showmenu $menuDeleteRecordTitle;
    ]
  ]

]

//
// Function that is called when the "Enable Gema Mode" checkbox is toggled.
// Tries to set the gema mode state to the value of the checkbox and refreshes the menu if it fails.
//
// @param int checkBoxState ($arg1) The current state of the "Enable Gema Mode" checkbox
//
const onEnableGemaModeCheckBoxToggled [
  gemaModeStateWasSet = (tryToSetCurrentGemaModeState $arg1);
  if (! $gemaModeStateWasSet) [
    // The state was not set but the checkbox state changed, refresh the menu to undo the checkbox change
    refreshMenu $menuGemaModeTitle 0;
  ]
]

//
// Shows the "Gema Mode" menu.
//
const showGemaModeMenu [
  showmenu $menuGemaModeTitle;
]


// Menu configuration

newmenu $menuGemaModeTitle;
menuitemcheckbox $menuItemEnableGemaModeText [ isGemaModeActive ][ onEnableGemaModeCheckBoxToggled ];
menuitem;
menuitemvar [ format $menuItemCurrentMap (curmap) ][ showmenu $menuMapsTitle ];
menuitemvar [ format $menuItemBestMapRecord (getBestMapRecordMenuString (curmap) $curmaprevision) ][ onMenuItemBestMapRecordSelected ];
menuitemvar [ format $menuItemCurrentTimeTarget (generateTimeStringFromTimeInterval (getCurrentTimeTarget) $colorsTimeTargetTime) ][ showmenu $menuTimeTargetTitle ];
menuitem;
menuitem $menuItemOptions [ showmenu $menuOptionsTitle ];
menuitem;
menuitem $menuItemCloseMenu [ closemenu $menuGemaModeTitle ];
