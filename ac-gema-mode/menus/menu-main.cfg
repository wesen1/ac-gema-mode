//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

//
// Configuration for the main menu
//
// Requires:
//   * scripts/ac-gema-mode/colors.cfg
//   * scripts/ac-gema-mode/custom-declarations.cfg
//   * scripts/ac-gema-mode/gema-mode-state/gema-mode-state.cfg
//   * scripts/ac-gema-mode/map-record/output/menus/menu-string-generator.cfg
//   * scripts/ac-gema-mode/map-record/storage/best-map-record-cache.cfg
//   * scripts/ac-gema-mode/menus/menu-delete-record.cfg
//   * scripts/ac-gema-mode/menus/menu-maps.cfg
//   * scripts/ac-gema-mode/menus/menu-options.cfg
//   * scripts/ac-gema-mode/menus/menu-time-target.cfg
//   * scripts/ac-gema-mode/menus/menu-weapon-map-records.cfg
//   * scripts/ac-gema-mode/output/tab-stop-calculator.cfg
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/time/time-formatter.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//   * scripts/ac-gema-mode/util/list-utils.cfg
//   * scripts/ac-gema-mode/util/menu-utils.cfg
//

// Options

//
// Option to enable the "Weapon Records" menu (Default: 1).
// If set to 0 selecting the best map record will result in a "Delete records" dialog
// that affects all map records of the current map at once
//
// @var int $optionEnableWeaponRecordsMenu
//
option optionEnableWeaponRecordsMenu 1;

persistidents 0;


// Global Variables

//
// The last calculated numbers of pad tabs for the tab padded main menu texts
//
// @var int[] $lastCalculatedMenuPadTabsForMainMenu
//
cache lastCalculatedMenuPadTabsForMainMenu [];


// Public Functions

//
// Calculates the menu pad tabs if required and returns the last calculated numbers of pad tabs for the tab padded main menu texts.
//
// @return int[] The last calculated numbers of pad tabs for the tab padded main menu texts
//
function getMenuPadTabsForMainMenu [
  formatMenusForFont (getMenuFontType);
  return $lastCalculatedMenuPadTabsForMainMenu;
]

//
// Calculates the menu pad tabs for the tab padded main menu texts.
//
// @param int fontType ($arg1) The font type to calculate the pad tabs for
//
function calculateMenuPadTabsForMainMenu [

  fontType = $arg1;

  tabPaddedMainMenuTexts = [];
  add2list tabPaddedMainMenuTexts (strreplace $menuItemCurrentMapTitle " " $TEMPORARY_WHITESPACE);
  add2list tabPaddedMainMenuTexts (strreplace $menuItemBestMapRecordTitle " " $TEMPORARY_WHITESPACE);
  add2list tabPaddedMainMenuTexts (strreplace $menuItemCurrentTimeTargetTitle " " $TEMPORARY_WHITESPACE);

  lastCalculatedMenuPadTabsForMainMenu = (calculateDistancesToClosestCommonNextTabStopNumber $tabPaddedMainMenuTexts 0 $fontType);

]


//
// Returns the text for the "Map: <map name>" menu item.
//
// @return string The text for the "Map: <map name>" menu item
//
function getMenuItemMapText [
  numberOfPadTabs = (at (getMenuPadTabsForMainMenu) 0);
  return (format $menuItemCurrentMap (strrepeat $TAB_CHARACTER $numberOfPadTabs) (curmap) $curmaprevision);
]

//
// Returns the text for the "Best time: <time>" menu item.
//
// @return string The text for the "Best time: <time>" menu item
//
function getMenuItemBestMapRecordText [
  numberOfPadTabs = (at (getMenuPadTabsForMainMenu) 1);
  return (format $menuItemBestMapRecord (strrepeat $TAB_CHARACTER $numberOfPadTabs) (getBestMapRecordMenuString (curmap) $curmaprevision));
]

//
// Returns the text for the "Time target: <time target>" menu item.
//
// @return string The text for the "Time target: <time target>" menu item
//
function getMenuItemCurrentTimeTargetText [
  numberOfPadTabs = (at (getMenuPadTabsForMainMenu) 2);
  return (format $menuItemCurrentTimeTarget (strrepeat $TAB_CHARACTER $numberOfPadTabs) (generateTimeStringFromTimeInterval (getCurrentTimeTarget) $colorsTimeTargetTime));
]


//
// Shows either the "Weapon Records" or "Delete Records" menu depending on $optionEnableWeaponRecordsMenu.
// This function is called when the best map record menu item is selected.
//
function onMenuItemBestMapRecordSelected [

  if ($optionEnableWeaponRecordsMenu) [
    showmenu $menuShowRecordsTitle;
  ][
    if (strcmp (getBestMapRecord (curmap) $curmaprevision) "") [
      // There is no record for the current map, do nothing
      -1;
    ][
      // There is at least one map record for the current map, show the "Delete all records" menu
      showDeleteRecordDialog (curmap) -1 -1;
    ]
  ]

]

//
// Function that is called when the "Enable Gema Mode" checkbox is toggled.
// Tries to set the gema mode state to the value of the checkbox and refreshes the menu if it fails.
//
// @param int checkBoxState ($arg1) The current state of the "Enable Gema Mode" checkbox
//
function onEnableGemaModeCheckBoxToggled [
  gemaModeStateWasSet = (tryToSetCurrentGemaModeState $arg1);
  if (! $gemaModeStateWasSet) [
    // The state was not set but the checkbox state changed, refresh the menu to undo the checkbox change
    refreshMenu $menuGemaModeTitle 0;
  ]
]

//
// Shows the "Gema Mode" menu.
//
function showGemaModeMenu [
  showmenu $menuGemaModeTitle;
]


// Menu configuration

addMenuFontFormatter calculateMenuPadTabsForMainMenu;

newmenu $menuGemaModeTitle;
menuitemcheckbox $menuItemEnableGemaModeText [ isGemaModeActive ][ onEnableGemaModeCheckBoxToggled ];
menuitem;
menuitemvar [ getMenuItemMapText ][ showmenu $menuMapsTitle ];
menuitemvar [ getMenuItemBestMapRecordText ][ onMenuItemBestMapRecordSelected ];
menuitemvar [ getMenuItemCurrentTimeTargetText ][ showmenu $menuTimeTargetTitle ];
menuitem;
menuitem $menuItemOptions [ showmenu $menuOptionsTitle ];
menuitem;
menuitem $menuItemCloseMenu [ closemenu $menuGemaModeTitle ];
