//
// @author wesen
// @copyright 2019 wesen <wesen-ac@web.de>
//

persistidents 0;

exec "scripts/ac-gema-mode/menus/menu-delete-record.cfg";
exec "scripts/ac-gema-mode/menus/menu-maps.cfg";
exec "scripts/ac-gema-mode/menus/menu-weapon-map-records.cfg";
exec "scripts/ac-gema-mode/menus/menu-time-target.cfg";
exec "scripts/ac-gema-mode/menus/menu-options.cfg";

//
// Configuration for the main menu
//
// Requires:
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/gema-mode-state/gema-mode-state.cfg
//   * scripts/ac-gema-mode/map-record/map-record-printer.cfg
//   * scripts/ac-gema-mode/map-record/map-statistics-printer.cfg
//   * scripts/ac-gema-mode/menus/menu-delete-record.cfg
//   * scripts/ac-gema-mode/menus/menu-maps.cfg
//   * scripts/ac-gema-mode/menus/menu-weapon-map-records.cfg
//   * scripts/ac-gema-mode/menus/menu-time-target.cfg
//   * scripts/ac-gema-mode/menus/menu-options.cfg
//   * scripts/ac-gema-mode/util/time-parser.cfg
//

//
// Shows either the "Weapon Records" or "Delete Records" menu depending on $optionShowScoreWeapon.
// This function is called when the best map record menu item is selected by the user.
//
const menuItemBestMapRecordSelectAction [

  if ($optionEnableWeaponRecordsMenu) [
    showmenu $menuShowRecordsTitle;
  ][
    if (!= (getBestMapRecordWeaponId (curmap)) -1) [
      showmenu $menuDeleteRecordTitle;
    ][
      -1;
    ]
  ]

]

//
// Shows the "Gema Mode" menu.
//
const showGemaModeMenu [
  showmenu $menuGemaModeTitle;
]


newmenu $menuGemaModeTitle
menuitemcheckbox $menuItemEnableGemaModeText [ isGemaModeActive ][ setGemaModeState $arg1 ]
menuitem
menuitemvar [ format $menuItemCurrentMap (curmap) ][ showmenu $menuMapsTitle ]
menuitemvar [ format $menuItemBestMapRecord (getBestMapRecordMenuString (curmap)) ][ menuItemBestMapRecordSelectAction ]
menuitemvar [ format $menuItemCurrentTimeTarget (generateTimeString $timeTarget 1) ][ showmenu $menuTimeTargetTitle ]
menuitem
menuitem $menuItemOptions [ showmenu $menuOptionsTitle ]
menuitem
menuitem $menuItemCloseMenu [ closemenu $menuGemaModeTitle ]
