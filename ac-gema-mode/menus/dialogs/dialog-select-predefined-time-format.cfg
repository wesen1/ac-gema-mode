//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Configuration for the "Select predefined time format" dialog
//
// Requires:
//   * scripts/ac-gema-mode/custom-declarations.cfg
//   * scripts/ac-gema-mode/menus/manager/dialog-manager.cfg
//   * scripts/ac-gema-mode/menus/manager/menu-formatter.cfg
//   * scripts/ac-gema-mode/output/output-utils.cfg
//   * scripts/ac-gema-mode/output/tab-stop-calculator.cfg
//   * scripts/ac-gema-mode/strings
//   * scripts/ac-gema-mode/time/time-format-manager.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//   * scripts/ac-gema-mode/unpersist/functions.cfg
//   * scripts/ac-gema-mode/util/string-utils.cfg
//

persistidents 0;


// Global variables

//
// The name of the event handler function that will be called when a predefined time format is selected.
// This is set when "showSelectPredefinedTimeFormatDialog" is called
//
// @var string $onPredefinedTimeFormatSelectedEventHandlerName
//
cache onPredefinedTimeFormatSelectedEventHandlerName "";


// Public Functions

//
// Shows the "Select predefined time format" dialog.
//
// @param string onPredefinedTimeFormatSelectedEventHandlerName ($arg1) The name of the event handler function to call when a predefined time format is selected
//
function showSelectPredefinedTimeFormatDialog [
  onPredefinedTimeFormatSelectedEventHandlerName = $arg1;
  showDialog "SelectPredefinedTimeFormat";
]


// Menu configuration

//
// Builds the menu for the "Select predefined time format" dialog.
//
function buildSelectPredefinedTimeFormatDialog [

  newmenu $dialogSelectPredefinedTimeFormatTitle;

  // Generate a menu item for each predefined time format
  padTabsForTimeFormatNames = (calculateDistancesToClosestCommonNextTabStopNumber $predefinedTimeFormatNames 0 (getMenuFontType));

  predefinedTimeFormatIndex = 0;
  ulooplist $predefinedTimeFormats predefinedTimeFormat [
    menuItemSelectAction = (format [closeDialog "SelectPredefinedTimeFormat"; %1 "%2"] $onPredefinedTimeFormatSelectedEventHandlerName $predefinedTimeFormat);

    predefinedTimeFormatName = (at $predefinedTimeFormatNames $predefinedTimeFormatIndex);
    numberOfPadTabs = (at $padTabsForTimeFormatNames $predefinedTimeFormatIndex);
    menuItemText = (format $menuItemPredefinedTimeFormatText $predefinedTimeFormatName (strrepeat $TAB_CHARACTER $numberOfPadTabs) $predefinedTimeFormat);

    execute (format "menuitem [%1][%2]" $menuItemText $menuItemSelectAction);

    ++ predefinedTimeFormatIndex;
  ]

  menuitem;
  menuitem $menuItemGoBackText [ closeDialog "SelectPredefinedTimeFormat" ];

]
