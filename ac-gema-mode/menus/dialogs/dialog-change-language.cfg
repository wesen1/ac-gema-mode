//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Configuration for the "Change Language" dialog
//
// Requires:
//   * scripts/ac-gema-mode/colors.cfg
//   * scripts/ac-gema-mode/language.cfg
//   * scripts/ac-gema-mode/menus/manager/dialog-manager.cfg
//   * scripts/ac-gema-mode/notification-printer.cfg
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//   * scripts/ac-gema-mode/unpersist/functions.cfg
//

persistidents 0;

// Public Functions

//
// Shows the "Change Language" dialog.
//
function showChangeLanguageDialog [
  showDialog "ChangeLanguage";
]

//
// Event handler that is called when a language is selected via the dialog.
//
// @param string languageIdentifier ($arg1) The identifier of the language that was selected
//
function onLanguageSelected [

  languageIdentifier = $arg1;
  if (setOptionLanguage $languageIdentifier) [
    languageFullName = (languageIdentifierToFullLanguageName $languageIdentifier);
    printGemaNotification (format $messageSuccessfullyChangedLanguage $languageFullName);
  ][
    printGemaNotification (format $messageFailedToChangeLanguage $languageFullName);
  ]

]


// Menu configuration

//
// Builds the menu for the "Change Language" dialog.
//
function buildChangeLanguageDialog [

  newmenu $dialogChangeLanguageTitle;

  currentLanguage = (getCurrentLanguage);
  currentLanguageIndex = (findlist $availableLanguages $currentLanguage);

  // Generate a menu item for each language
  languageIndex = 0;
  ulooplist (getAvailableLanguages) languageIdentifier [

    languageFullName = (languageIdentifierToFullLanguageName $languageIdentifier);

    if (strcmp $languageIdentifier $currentLanguage) [
      menuItemSelectAction = [ printGemaNotification $messageSelectedLanguageIsCurrentLanguage; return -1 ];
      menuItemTextColor = $colorCurrentLanguage;
    ][
      menuItemSelectAction = (format "onLanguageSelected %1" $languageIdentifier);
      menuItemTextColor = $colorLanguage;
    ]

    menuItemText = (concatword (c $menuItemTextColor) $languageFullName);
    execute (format "menuitem [%1][%2]" $menuItemText $menuItemSelectAction);

    ++ languageIndex;
  ]

  menuitem;
  menuitem $menuItemGoBackText [ closeDialog "ChangeLanguage" ];

  // Initially select the current active language
  menuselection $dialogChangeLanguageTitle $currentLanguageIndex;

]
