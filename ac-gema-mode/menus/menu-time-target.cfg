//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

//
// Configuration for the time target menu
//
// Requires:
//   * scripts/ac-gema-mode/menus/manager/menu-manager.cfg
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/time-target-mode/count-down.cfg
//   * scripts/ac-gema-mode/time-target-mode/count-down-mode/current-count-down-mode-state.cfg
//   * scripts/ac-gema-mode/time-target-mode/current-time-target-mode-state.cfg
//   * scripts/ac-gema-mode/time-target-mode/time-target.cfg
//   * scripts/ac-gema-mode/time-target-mode/time-target-mode.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//

persistidents 0;


// Public Functions

//
// Function that is called when the "Enable Time Target Mode" checkbox is toggled.
// Tries to set the time target mode state to the value of the checkbox and refreshes the menu.
//
// @param int checkBoxState ($arg1) The current state of the "Enable Time Target Mode" checkbox
//
function onEnableTimeTargetModeCheckBoxToggled [

  tryToSetCurrentTimeTargetModeState $arg1;

  // Must always refresh the menu because the countdown mode checkbox depends on the time target mode state
  softRefreshMenu $menuTimeTargetTitle 0;

]

//
// Function that is called when the "Show countdown" checkbox is toggled.
// Tries to set the countdown mode state to the value of the checkbox and refreshes the menu if it fails.
//
// @param int checkBoxState ($arg1) The current state of the "Show countdown" checkbox
//
function onShowCountdownCheckBoxToggled [

  countdownModeStateWasSet = (tryToSetCurrentCountdownModeState $arg1);
  if (! $countdownModeStateWasSet) [
    // The state was not set but the checkbox state changed, refresh the menu to undo the checkbox change
    softRefreshMenu $menuTimeTargetTitle 1;
  ]

]

//
// Function that is called when the "Minutes" text input field value changed.
// Tries to set the time target minutes to the value of the input field and refreshes the menu if it fails.
//
// @param int inputFieldValue ($arg1) The current value of the "Minutes" input field
//
function onTimeTargetMinutesEdited [
  setCurrentTimeTargetMinutes $arg1;
  if (!= (getCurrentTimeTargetMinutes) $arg1) [
    softRefreshMenu $menuTimeTargetTitle 3;
  ]
]

//
// Function that is called when the "Seconds" text input field value changed.
// Tries to set the time target seconds to the value of the input field and refreshes the menu if it fails.
//
// @param int inputFieldValue ($arg1) The current value of the "Seconds" input field
//
function onTimeTargetSecondsEdited [
  setCurrentTimeTargetSeconds $arg1;
  if (!= (getCurrentTimeTargetSeconds) $arg1) [
    softRefreshMenu $menuTimeTargetTitle 4;
  ]
]

//
// Function that is called when the "Milliseconds" text input field value changed.
// Tries to set the time target milliseconds to the value of the input field and refreshes the menu if it fails.
//
// @param int inputFieldValue ($arg1) The current value of the "Milliseconds" input field
//
function onTimeTargetMillisecondsEdited [
  setCurrentTimeTargetMilliseconds $arg1;
  if (!= (getCurrentTimeTargetMilliseconds) $arg1) [
    softRefreshMenu $menuTimeTargetTitle 5;
  ]
]


// Menu configuration

//
// Builds the "Time Target" menu.
//
function buildTimeTargetMenu [

  menuitemcheckbox $menuItemEnableTimeTargetMode [ isTimeTargetModeActive ][ onEnableTimeTargetModeCheckBoxToggled ];
  menuitemcheckbox $menuItemShowCountdown [ isCountdownModeActive ][ onShowCountdownCheckBoxToggled ];
  menuitem;
  menuitemtextinput $menuItemTimeTargetMinutes [ getCurrentTimeTargetMinutes ][ onTimeTargetMinutesEdited ];
  menuitemtextinput $menuItemTimeTargetSeconds [ getCurrentTimeTargetSeconds ][ onTimeTargetSecondsEdited ];
  menuitemtextinput $menuItemTimeTargetMilliseconds [ getCurrentTimeTargetMilliseconds ][ onTimeTargetMillisecondsEdited ];
  menuitem;
  menuitem $menuItemGoBackText [ closemenu $menuTimeTargetTitle ];

]
