//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

persistidents 0;

//
// Configuration for the time target menu
//
// Requires:
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/time-target-mode/time-target-mode.cfg
//   * scripts/ac-gema-mode/time-target-mode/time-target.cfg
//   * scripts/ac-gema-mode/time-target-mode/time-target-auto-updater.cfg
//   * scripts/ac-gema-mode/time-target-mode/count-down.cfg
//   * scripts/ac-gema-mode/util/menu-utils.cfg
//

//
// Function that is called when the "Enable Time Target Mode" checkbox is toggled.
// Tries to set the time target mode state to the value of the checkbox and refreshes the menu if it fails.
//
// @param int checkBoxState ($arg1) The current state of the "Enable Time Target Mode" checkbox
//
const onEnableTimeTargetModeCheckBoxToggled [

  tryToSetCurrentTimeTargetModeState $arg1;

  // Must always refresh the menu because the countdown mode checkbox depends on the time target mode state
  refreshMenu $menuTimeTargetTitle 0;

]

//
// Function that is called when the "Show countdown" checkbox is toggled.
// Tries to set the countdown mode state to the value of the checkbox and refreshes the menu if it fails.
//
// @param int checkBoxState ($arg1) The current state of the "Show countdown" checkbox
//
const onShowCountDownCheckBoxToggled [

  countDownModeStateWasSet = (tryToSetCurrentCountDownModeState $arg1);
  if (! $countDownModeStateWasSet) [
    // The state was not set but the checkbox state changed, refresh the menu to undo the checkbox change
    refreshMenu $menuTimeTargetTitle 1;
  ]

]

//
// Function that is called when the "Minutes" text input field value changed.
// Tries to set the time target minutes to the value of the input field and refreshes the menu if it fails.
//
// @param int inputFieldValue ($arg1) The current value of the "Minutes" input field
//
const onTimeTargetMinutesEdited [

  timeTargetSetMinutes $arg1;

  if (!= (timeTargetGetMinutes) $arg1) [
    refreshMenu $menuTimeTargetTitle 3;
  ]

]

//
// Function that is called when the "Seconds" text input field value changed.
// Tries to set the time target seconds to the value of the input field and refreshes the menu if it fails.
//
// @param int inputFieldValue ($arg1) The current value of the "Seconds" input field
//
const onTimeTargetSecondsEdited [

  timeTargetSetSeconds $arg1;

  if (!= (timeTargetGetSeconds) $arg1) [
    refreshMenu $menuTimeTargetTitle 4;
  ]

]

//
// Function that is called when the "Milliseconds" text input field value changed.
// Tries to set the time target milliseconds to the value of the input field and refreshes the menu if it fails.
//
// @param int inputFieldValue ($arg1) The current value of the "Milliseconds" input field
//
const onTimeTargetMillisecondsEdited [

  timeTargetSetMilliseconds $arg1;

  if (!= (timeTargetGetMilliseconds) $arg1) [
    refreshMenu $menuTimeTargetTitle 5;
  ]

]


newmenu $menuTimeTargetTitle
menuitemcheckbox $menuItemEnableTimeTargetMode [ isTimeTargetModeActive ][ onEnableTimeTargetModeCheckBoxToggled ]
menuitemcheckbox $menuItemShowCountDown [ isCountDownModeActive ][ onShowCountDownCheckBoxToggled ]
menuitem
menuitemtextinput $menuItemTimeTargetMinutes [ timeTargetGetMinutes ][ onTimeTargetMinutesEdited ]
menuitemtextinput $menuItemTimeTargetSeconds [ timeTargetGetSeconds ][ onTimeTargetSecondsEdited ]
menuitemtextinput $menuItemTimeTargetMilliseconds [ timeTargetGetMilliseconds ][ onTimeTargetMillisecondsEdited ]
menuitem
menuitem $menuItemGoBackText [ closemenu $menuTimeTargetTitle ]
