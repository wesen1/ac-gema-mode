//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Menu formatter module for ac-gema-mode
//
// Provides functions to manage the formatting of the menus based on the current font.
//
// Requires:
//   * scripts/ac-gema-mode/custom-declarations.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//   * scripts/ac-gema-mode/unpersist/functions.cfg
//

// Options

//
// The font type that should be used to format menus
// If -1 the font type will be dynamically read from $__fontsetting.
// If >= 0 the font type will be fixed to the $optionMenuFontType value
//
// @var int $optionMenuFontType
//
option optionMenuFontType -1;

persistidents 0;


// Global Variables

//
// The last font type that the menus were formatted for
//
// @var int $lastMenuFormatFontType
//
cache lastMenuFormatFontType -1;

//
// The list of menu font formatting functions
// These functions will be called every time the formatMenusForFont function reformats the menus
//
// @var string[] $menuFontFormatters
//
cache menuFontFormatters [];


// Public Functions

//
// Formats the menus for a given font if the given font type differs from the $lastMenuFormatFontType.
// This is done by calling the $menuFontFormatters.
//
// @param int fontType ($arg1) The font type to format the menus for
//
function formatMenusForFont [

  fontType = $arg1;
  if (!= $lastMenuFormatFontType $fontType) [
    ulooplist $menuFontFormatters menuFontFormatter [
      $menuFontFormatter $fontType;
    ]

    lastMenuFormatFontType = $fontType;
  ]

]

//
// Invalidates the menu format cache.
//
function invalidateMenuFormatCache [
  lastMenuFormatFontType = -1;
]

//
// Adds a menu font formatting function.
//
// @param string menuFontFormatter ($arg1) The name of the menu font formatting function to add
//
function addMenuFontFormatter [
  add2list menuFontFormatters $arg1;
]


//
// Returns the font type that should be used for formatting menus.
//
// @return int The menu font type
//
function getMenuFontType [

  if (= $optionMenuFontType -1) [
    return $__fontsetting;
  ][
    return $optionMenuFontType;
  ]

]

//
// Returns the value names for the possible values of the menu font type option.
//
// @return string[] The value names for the possible values of the menu font type option
//
function getAvailableMenuFontTypes [
  return (concat "auto" (getAvailableFontTypes));
]
