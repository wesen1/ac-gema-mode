//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Dialog manager module for ac-gema-mode
//
// Provides functions to build and rebuild dialogs.
// Dialogs are menus that are deleted on close.
//
// The titles of dialogs must be stored in variables named "dialog<DialogName>Title".
// Also all dialogs must provide a "build<DialogName>Dialog" function to build the dialog.
//
// Requires:
//   * scripts/ac-gema-mode/custom-declarations.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//   * scripts/ac-gema-mode/unpersist/functions.cfg
//

persistidents 0;


// Global Variables

//
// The names of the dialogs that were built and not deleted yet
//
// @var string[] $builtDialogNames
//
cache builtDialogNames []


// Public Functions

//
// Shows a specific dialog.
// Also deletes the old version of the dialog if required.
//
// @param string dialogName ($arg1) The name of the dialog to show
//
function showDialog [

  dialogName = $arg1;
  buildDialogFunctionName = (format "build%1Dialog" $dialogName);
  dialogTitle = (getDialogTitle $dialogName);

  if (!= (findlist $builtDialogNames $dialogName) -1) [
    delmenu $dialogTitle;
  ][
    add2list builtDialogNames $dialogName;
  ]

  $buildDialogFunctionName;
  showmenu $dialogTitle;

]

//
// Closes a specific dialog.
//
// @param string dialogName ($arg1) The name of the dialog to close
//
function closeDialog [
  // Cannot delete the dialog here because that crashes the game
  closemenu (getDialogTitle $arg1);
]

//
// Deletes all dialogs that are currently built and that were not deleted yet.
//
function deleteBuiltDialogs [

  ulooplist $builtDialogNames builtDialogName [
    delmenu (getDialogTitle $builtDialogName);
  ]
  builtDialogNames = [];

]


// Private Functions

//
// Returns the title for a given dialog.
//
// @param string dialogName ($arg1) The name of the dialog whose title to return
//
// @return string The title of the dialog
//
function getDialogTitle [
  return (getalias (format "dialog%1Title" $arg1));
]
