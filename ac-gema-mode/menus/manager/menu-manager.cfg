//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Menu manager module for ac-gema-mode
//
// Provides functions to build and rebuild menus.
//
// The titles of menus must be stored in variables named "menu<MenuName>Title".
// Also all menus must provide a "build<MenuName>Menu" function to build the menu.
//
// Requires:
//   * scripts/ac-gema-mode/custom-declarations.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//   * scripts/ac-gema-mode/unpersist/functions.cfg
//   * scripts/ac-gema-mode/util/list-utils.cfg
//

persistidents 0;


// Global Variables

//
// The names of the menus that were built and not deleted yet
//
// @var string[] $builtMenuNames
//
cache builtMenuNames [];

//
// The names of the menus that should be rebuilt the next time they are shown
//
// @var string[] $menusToRebuildOnNextShow
//
cache menusToRebuildOnNextShow [];


// Public Functions

//
// Shows a given menu.
//
// @param string menuName ($arg1) The name of the menu to show
//
function showMenu [

  showmenu_menuName = $arg1;
  if (= (findlist $builtMenuNames $showmenu_menuName) -1) [
    // The menu was not built yet, build it now
    buildMenu $showmenu_menuName;
  ][
    rebuildMenuIfRequired $showmenu_menuName;
  ]

  showmenu (getMenuTitle $showmenu_menuName);

]

//
// Adds a given menu to the list of menus that should be rebuilt the next time they are shown.
//
// @param string menuName ($arg1) The name of the menu to rebuild the next time it is shown
//
function rebuildMenuOnNextShow [
  add2list menusToRebuildOnNextShow $arg1;
]


//
// Reloads a menu and selects a specified line in the menu.
// This can be used to properly update checkboxes that the user is not allowed to activate or to
// reset text input field values when invalid input text is inserted.
//
// @param string menuName ($arg1) The name of the menu to soft refresh
// @param int lineNumber ($arg2) The line number to select after the refresh
//
function softRefreshMenu [

  softrefreshmenu_menuTitle = (getMenuTitle $arg1);
  selectedLineNumber = $arg2;

  closemenu $softrefreshmenu_menuTitle;
  showmenu $softrefreshmenu_menuTitle;
  menuselection $softrefreshmenu_menuTitle $selectedLineNumber;

]

//
// Hard refreshes a menu by rebuilding it.
//
// @param string menuName ($arg1) The name of the menu to hard refresh
// @param int menuSelectItem ($arg2) The line number to select after the refresh (optional)
//
function hardRefreshMenu [

  hardrefresh_menuName = $arg1;
  if (= $numargs 2) [
    hardrefresh_menuSelectItem = $arg2;
  ][
    hardrefresh_menuSelectItem = -1;
  ]

  hardrefresh_menuTitle = (getMenuTitle $hardrefresh_menuName);
  closemenu $hardrefresh_menuTitle;

  rebuildMenu $hardrefresh_menuName;

  showmenu $hardrefresh_menuTitle;

  if (> $hardrefresh_menuSelectItem -1) [
    menuselection $hardrefresh_menuTitle $hardrefresh_menuSelectItem;
  ]

]


//
// Deletes all currently built menus.
//
function deleteAllBuiltMenus [

  ulooplist $builtMenuNames builtMenuName [
    delmenu (getMenuTitle $builtMenuName);
  ]

  builtMenuNames = [];
  menusToRebuildOnNextShow = [];

]


// Private Functions

//
// Builds a given menu.
//
// @param string menuName ($arg1) The name of the menu to build
//
function buildMenu [

  menuName = $arg1;
  add2list builtMenuNames $showmenu_menuName;
  menusToRebuildOnNextShow = (listremove $menusToRebuildOnNextShow $menuName);

  menuinitTemplate = [
    rebuildMenuIfRequired %1 1;
    if (isIdent %2) [
      %2;
    ]
  ]
  onBeforeMenuShowEventHandlerName = (format "onBefore%1MenuShow" $menuName);

  newmenu (getMenuTitle $menuName);
  menuinit (format $menuinitTemplate $menuName $onBeforeMenuShowEventHandlerName);

  buildMenuFunctionName = (format "build%1Menu" $arg1);
  $buildMenuFunctionName;

]

//
// Rebuilds a given menu if it is contained in the list of menus that should be rebuild the next time they are shown.
//
// @param string menuName ($arg1) The name of the menu to rebuild if required
// @param int doHardRefresh ($arg2) 1 to do a hard refresh, 0 to only rebuild the menu
//
function rebuildMenuIfRequired [

  menuName = $arg1;
  doHardRefresh = (&& (= $numargs 2) (= $arg2 1));
  if (!= (findlist $menusToRebuildOnNextShow $menuName) -1) [
    // The menu should be rebuilt
    if ($doHardRefresh) [
      hardRefreshMenu $menuName;
    ][
      rebuildMenu $menuName;
    ]
  ]

]

//
// Rebuilds a given menu.
//
// @param string menuName ($arg1) The name of the menu to rebuild
//
function rebuildMenu [
  menuName = $arg1;

  delmenu (getMenuTitle $menuName);
  buildMenu $menuName;
]

//
// Returns the title of a given menu.
//
// @param string menuName ($arg1) The name of the menu whose title to return
//
// @return string The title of the given menu
//
function getMenuTitle [
  return (getalias (format "menu%1Title" $arg1));
]
