//
// @author wesen
// @copyright 2019 wesen <wesen-ac@web.de>
//

persistidents 0;

//
// Configuration for the options menu
//
// Requires:
//   * scripts/ac-gema-mode/strings.cfg
//

//
// Generates a check box menu item for options that can only be set to either true or false.
//
// @param string $arg1 The name of the option
//
const generateOptionCheckBoxMenuItem [

  menuItemName = (getalias (concatword "menuItem" $arg1 "Option"));
  optionAliasName = (concatword "option" $arg1);

  execute (format "menuitemcheckbox [%1][%2][%3]" $menuItemName (concatword "$" $optionAliasName) (concat $optionAliasName "= $arg1"));

]


newmenu $menuOptionsTitle

menuitem $menuItemGeneralOptionsTitle [ -1 ]
generateOptionCheckBoxMenuItem "AutoActivateGemaMode";
generateOptionCheckBoxMenuItem "ShowNotifications";
generateOptionCheckBoxMenuItem "ShowNameOnScore";
menuitem

menuitem $menuItemTimeTargetOptionsTitle [ -1 ]

menuitemslider $menuItemAutoSetTimeTargetModeOption 0 3 "$optionAutoSetTimeTargetMode" 1 $menuItemAutoSetTimeTargetModeValues [ setOptionAutoSetTimeTargetMode $arg1 ]
generateOptionCheckBoxMenuItem "ResetTimeTargetOnQuit";
menuitem

menuitem $menuItemWeaponOptionsTitle [ -1 ]
generateOptionCheckBoxMenuItem "ShowScoreWeapon";
generateOptionCheckBoxMenuItem "ShowSecondaryWeaponModeAbortedMessages";
generateOptionCheckBoxMenuItem "ShowMissingWeapons";

// TODO: Add the options
//menuitemcheckbox (concat (c J) "Show weapon system notifications" [ optionShowWeaponSystemNotifications = $arg1 ] // TODO: Move string to string.cfg and colors to colors.cfg

menuitem
menuitem $menuItemGoBackText [ closemenu $menuOptionsTitle ]
