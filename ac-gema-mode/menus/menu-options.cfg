//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

persistidents 0;

//
// Configuration for the options menu
//
// Requires:
//   * scripts/ac-gema-mode/strings.cfg
//

//
// Generates a check box menu item for options that can only be set to either true or false.
// For this to work the following conditions must be met:
//   1. The name of the option must be stored in a alias named "menuItem<optionName>Option"
//   2. The option's value must be stored in a alias named "option<optionName>"
//
// @param string optionName ($arg1) The name of the option
//
const generateOptionCheckBoxMenuItem [

  menuItemName = (getalias (concatword "menuItem" $arg1 "Option"));
  optionAliasName = (concatword "option" $arg1);

  execute (format "menuitemcheckbox [%1][%2][%3]" $menuItemName (concatword "$" $optionAliasName) (concat $optionAliasName "= $arg1"));

]


newmenu $menuOptionsTitle

menuitem $menuItemGeneralOptionsTitle [ -1 ]
generateOptionCheckBoxMenuItem "AutoActivateGemaMode";
generateOptionCheckBoxMenuItem "ShowNotifications";
menuitemslider $menuItemScoreTimeCompareValueOption 0 1 "$optionScoreTimeCompareValue" 1 $menuItemScoreTimeCompareValueOptionValues [ optionScoreTimeCompareValue = $arg1 ]
generateOptionCheckBoxMenuItem "ShowNameOnScore";
generateOptionCheckBoxMenuItem "ShowScoreWeapon";
menuitem

menuitem $menuItemTimeTargetOptionsTitle [ -1 ]

menuitemslider $menuItemAutoSetTimeTargetModeOption 0 3 "$optionAutoSetTimeTargetMode" 1 $menuItemAutoSetTimeTargetModeValues [ setOptionAutoSetTimeTargetMode $arg1 ]
generateOptionCheckBoxMenuItem "ResetTimeTargetOnQuit";
menuitem

menuitem $menuItemWeaponOptionsTitle [ -1 ]
generateOptionCheckBoxMenuItem "EnableWeaponRecordsMenu";
generateOptionCheckBoxMenuItem "ShowSecondaryWeaponModeAbortedMessages";
generateOptionCheckBoxMenuItem "ShowMissingWeapons";

menuitem
menuitem $menuItemGoBackText [ closemenu $menuOptionsTitle ]
