//
// @author wesen
// @copyright 2019 wesen <wesen-ac@web.de>
//

persistidents 0;

//
// Configuration for the "Delete Record" menu
//
// Requires:
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/map-record/storage/map-record-storage.cfg
//   * scripts/ac-gema-mode/util/weapon-name-fetcher.cfg
//

//
// The deletion target weapon id for the delete record dialog
// -1 targets all map records for a map
//
// @var int $deleteRecordWeaponId
//
checkinit deleteRecordWeaponId -1;


//
// Returns the confirm question for the "Delete Record" menu.
// The returned text depends on the deletion target (all records or a single record).
//
// @param String $arg1 The map name
//
// @return String The confirm question for the "Delete Record" menu
//
const getDeleteRecordConfirmQuestion [

  mapName = $arg1;

  if (= $deleteRecordWeaponId -1) [
    // Delete all records
    return (format $menuItemDeleteAllRecordsConfirmQuestion $mapName);
  ][
    // Delete a single record
    return (format $menuItemDeleteWeaponRecordConfirmQuestion $mapName (getWeaponName $deleteRecordWeaponId));
  ]

]

//
// Returns the text for the "Confirm" menu item in the "Delete Record" menu.
// The returned text depends on the deletion target (all records or a single record).
//
// @return String The text for the "Confirm" menu item in the "Delete Record" menu
//
const getDeleteRecordConfirmText [

  if (= $deleteRecordWeaponId -1) [
    return $menuItemDeleteAllRecordsConfirm;
  ][
    return $menuItemDeleteWeaponRecordConfirm;
  ]

]

//
// Returns the text for the "Cancel" menu item in the "Delete Record" menu.
// The returned text depends on the deletion target (all records or a single record).
//
// @return String The text for the "Cancel" menu item in the "Delete Record" menu
//
const getDeleteRecordCancelText [

  if (= $deleteRecordWeaponId -1) [
    return $menuItemDeleteAllRecordsCancel;
  ][
    return $menuItemDeleteWeaponRecordCancel;
  ]

]

//
// Deletes either a single map record or all map records depending on the deletion target weapon id.
// This function is called when the record deletion is confirmed by the user.
//
// @param String $arg1 The map name
//
const deleteRecordConfirmAction [

  mapName = $arg1;

  if (= $deleteRecordWeaponId -1) [
    deleteAllMapRecords $mapName;
  ][
    deleteMapRecord $mapName $deleteRecordWeaponId;
  ]

  // Reset the deletion target weapon id
  deleteRecordWeaponId = -1;

  closemenu $menuDeleteRecordTitle;

]

//
// Closes the "Delete Record" menu.
// This function is called when the record deletion is cancelled by the user.
//
const deleteRecordCancelAction [
  closemenu $menuDeleteRecordTitle;
]


newmenu $menuDeleteRecordTitle
menuitemvar [ getDeleteRecordConfirmQuestion (curmap) ]
menuitem
menuitemvar [ getDeleteRecordConfirmText ][ deleteRecordConfirmAction (curmap) ]
menuitemvar [ getDeleteRecordCancelText ][ deleteRecordCancelAction ]

// Initially select the cancel option
menuinitselection 3;
