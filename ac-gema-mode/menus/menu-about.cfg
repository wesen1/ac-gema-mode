//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Configuration for the about menu
//
// Requires:
//   * scripts/ac-gema-mode/custom-declarations.cfg
//   * scripts/ac-gema-mode/output/output-utils.cfg
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//   * scripts/ac-gema-mode/util/menu-utils.cfg
//   * scripts/ac-gema-mode/util/value-persistor.cfg
//   * scripts/ac-gema-mode/version-info.cfg
//

// Options

//
// Option for the maximum line width per menu item in the about menu (Default: 2000).
//
// @var int $optionMenuAboutMaximumLineWidth
//
option optionMenuAboutMaximumLineWidth 2000;

persistidents 0;


// Global Variables

//
// Stores whether the "About" menu should be rebuilt the next time it is shown
//
// @var int $rebuildAboutMenuOnNextShow
//
cache rebuildAboutMenuOnNextShow 0;


// Public Functions

//
// Menu font formatter that reformats the about menu for the current font.
// This just makes the about menu rebuild itself the next time it is shown.
//
function formatAboutMenuForFont [
  rebuildAboutMenuOnNextShow = 1;
]

//
// Sets the current value of the $optionMenuAboutMaximumLineWidth option.
//
// @param int newOptionValue ($arg1) The new value for the $optionMenuAboutMaximumLineWidth option
//
function setOptionMenuAboutMaximumLineWidth [

  // Cast the newOptionValue to a integer
  newMenuAboutMaximumLineWidth = (+ $arg1 0);

  if (!= $newMenuAboutMaximumLineWidth $optionMenuAboutMaximumLineWidth) [
    setPersistentValue optionMenuAboutMaximumLineWidth $newMenuAboutMaximumLineWidth;
    rebuildAboutMenuOnNextShow = 1;
  ]

]


// Event Handlers

//
// Event handler that is called before the "About" menu is shown.
// Rebuilds the menu if required.
//
function onBeforeAboutMenuShow [

  if ($rebuildAboutMenuOnNextShow) [
    closemenu $menuAboutTitle;

    rebuildAboutMenuOnNextShow = 0;
    rebuildAboutMenu;

    showmenu $menuAboutTitle;
  ]

]


// Menu configuration

addMenuFontFormatter formatAboutMenuForFont;

//
// Builds the "About" menu.
//
function buildAboutMenu [

  versionInfoText = (format $menuAboutAcGemaModeVersionInfoText $AC_GEMA_MODE_VERSION);

  if (= (listlen $AC_GEMA_MODE_AUTHORS) 1) [
    authorInfoText = (format $menuAboutAcGemaModeSingleAuthorInfoText $AC_GEMA_MODE_AUTHORS);
  ][
    authorsOutputList = (generateOutputList $AC_GEMA_MODE_AUTHORS $menuAboutAcGemaModeAuthorsListSeparator $menuAboutAcGemaModeAuthorsListLastSeparator);
    authorInfoText = (format $menuAboutAcGemaModeMultipleAuthorsInfoText $authorsOutputList);
  ]


  newmenu $menuAboutTitle;
  menuinit onBeforeAboutMenuShow;

  generateTextMenuItems $menuAboutGemaDescriptionHeader $optionMenuAboutMaximumLineWidth $menuAboutHeaderPrefix;
  generateTextMenuItems $menuAboutGemaDescriptionText $optionMenuAboutMaximumLineWidth $menuAboutParagraphPrefix;
  menuitem;

  generateTextMenuItems $menuAboutGemaRulesHeader $optionMenuAboutMaximumLineWidth $menuAboutHeaderPrefix;
  generateTextMenuItems $menuAboutGemaRulesText $optionMenuAboutMaximumLineWidth $menuAboutParagraphPrefix;
  menuitem;

  generateTextMenuItems $menuAboutAcGemaModeHeader $optionMenuAboutMaximumLineWidth $menuAboutHeaderPrefix;
  generateTextMenuItems $versionInfoText $optionMenuAboutMaximumLineWidth $menuAboutParagraphPrefix;
  generateTextMenuItems $authorInfoText $optionMenuAboutMaximumLineWidth $menuAboutParagraphPrefix;
  menuitem;

  menuitem $menuItemGoBackText [ closemenu $menuAboutTitle ];

]

//
// Deletes and rebuilds the "About" menu.
//
function rebuildAboutMenu [
  delmenu $menuAboutTitle;
  buildAboutMenu;
]


buildAboutMenu;
