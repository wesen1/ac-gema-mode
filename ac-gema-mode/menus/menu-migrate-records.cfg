//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Configuration for the "Migrate Records" menu
//
// Requires:
//   * scripts/ac-gema-mode/map-record/map-record-migrator.cfg
//   * scripts/ac-gema-mode/map-record/storage/map-revision-storage.cfg
//   * scripts/ac-gema-mode/notification-printer.cfg
//   * scripts/ac-gema-mode/strings.cfg
//

persistidents 0;


// Global Variables

//
// The target map name for the "Migrate Records" dialog
//
// @var string $migrateRecordsMapName
//
checkinit migrateRecordsMapName "";

//
// The target map revision for the "Migrate Records" dialog
//
// @var int $migrateRecordsMapRevision
//
checkinit migrateRecordsMapRevision "";


// Public Functions

//
// Shows the "Migrate Records" menu.
//
// @param string mapName ($arg1) The map name
// @param int mapRevision ($arg2) The map revision
//
const showMigrateRecordsMenu [

  migrateRecordsMapName = $arg1;
  migrateRecordsMapRevision = $arg2;

  showmenu $menuMigrateRecordsTitle;

]

//
// Migrates all records from the target map revision to the current map revision.
// Also closes the "Migrate Records" menu.
//
// This function is called when the record migration is confirmed by the user.
//
const migrateRecordsConfirmAction [

  currentMapRevision = (getCurrentMapRevision $migrateRecordsMapName);

  migrateMapRecordsFromRevisionTo $migrateRecordsMapName $migrateRecordsMapRevision $currentMapRevision;
  printGemaNotification (format $messageRecordsMigrated $migrateRecordsMapName $migrateRecordsMapRevision $currentMapRevision);
  closemenu $menuMigrateRecordsTitle;

]

//
// Closes the "Migrate Records" menu.
// This function is called when the record migration is cancelled by the user.
//
const migrateRecordsCancelAction [
  closemenu $menuMigrateRecordsTitle;
]


// Menu configuration

newmenu $menuMigrateRecordsTitle;
menuitemvar [ format $menuItemMigrateRecordsConfirmQuestion $migrateRecordsMapName $migrateRecordsMapRevision (getCurrentMapRevision $migrateRecordsMapName) ];
menuitem;
menuitem $menuItemMigrateRecordsConfirm [ migrateRecordsConfirmAction ];
menuitem $menuItemMigrateRecordsCancel [ migrateRecordsCancelAction ];

// Initially select the cancel option
menuinitselection 3;
