//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Unpersistent declarations module for ac-gema-mode
//
// Provides various ways to declare variables and functions with non persistent contents.
// This is needed to keep saved.cfg free from variables that are used inside functions.
//

persistidents 0;


// Public Functions

//
// Creates a new function with non persistent variables inside the function body.
//
// @param string functionName ($arg1) The name of the function
// @param string functionBody ($arg2) The function body
//
const function [
  const $arg1 (concatword "persistidents 0;" $arg2);
]

//
// Creates a new custom event.
// Event listeners for the event can be added using the eventlistener function.
//
// @param string eventName ($arg1) The event name
// @param string eventBody ($arg2) The event body
//
function event [
  checkinit $arg1 (concatword "persistidents 0;" $arg2);
]

//
// Creates a new event listener for a event.
//
// @param string eventName ($arg1) The event name
// @param string eventListenerBody ($arg2) The event listener body
//
function eventlistener [
  checkinit $arg1 (concatword ";persistidents 0;" $arg2 ";persistidents 1;");
]
