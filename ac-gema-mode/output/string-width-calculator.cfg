//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// String width calculator module for ac-gema-mode
//
// Provides functions to calculate the width of given strings for specified font types.
//
// Requires:
//   * scripts/ac-gema-mode/output/character-width-loader.cfg
//   * scripts/ac-gema-mode/output/tab-stop-calculator.cfg
//   * scripts/ac-gema-mode/util/list-utils.cfg
//

persistidents 0;


// Public Functions

//
// Calculates the width of a given string for a specified font type.
//
// @param string targetString ($arg1) The string whose width to calculate
// @param int fontType ($arg2) The font type to calculate the width for
//
// @return int The calculated width
//
const calculateStringWidth [

  targetString = (strreplace $arg1 $TEMPORARY_WHITESPACE " ");
  fontType = $arg2;

  targetStringWidth = 0;

  skipNextCharacter = 0;
  parsestring $targetString targetStringCharacter [

    if ($skipNextCharacter) [
      skipNextCharacter = 0;
    ][
      if (|| (strcmp $targetStringCharacter $COLOR_CHARACTER) (strcmp $targetStringCharacter $BELL_CHARACTER)) [
        skipNextCharacter = 1;
      ][

        if (strcmp $targetStringCharacter $TAB_CHARACTER) [
          targetStringWidth = (calculateNextTabStopNumberPosition $targetStringWidth $fontType);
        ][
          targetStringCharacterWidth = (loadCharacterWidthFromFontType $targetStringCharacter $fontType);
          targetStringWidth = (+ (+ $targetStringWidth $targetStringCharacterWidth) 1);
        ]

      ]
    ]

  ]

  return $targetStringWidth;

]
