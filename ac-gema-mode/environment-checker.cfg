//
// @author wesen
// @copyright 2017-2018 wesen <wesen-ac@web.de>
//

//
// Environment checker module for ac-gema-mode
//

check2init gemaModeActive 0;  // De-/activates the gema mode

//
// Sets the gemaModeActive variable.
//
// @param int $arg1 The state to which the gema mode will be set
//
const setGemaModeActive [

  if (|| (isGema) (! $arg1)) [
    gemaModeActive = $arg1;
    if (! $arg1) [
      abortRun;
    ]
  ][
      printGemaNotification (concat (c $colorGemaModeDeactivated) "Can not change gema mode state: Map is not a gema or mode is not CTF.");
  ]

]

//
// Returns whether the gema mode is currently active.
//
// @return int 0: Gema mode is not active
//             1: Gema mode is active
//
const isGemaModeActive [
  return (&& $gemaModeActive (isGema));
]

//
// Returns whether the current environment is gema compatible.
//
// @return int 0: Environment is not gema compatible
//             1: Environment is gema compatible
//
const isGema [

  if (&& (strcmp (getmode) "ctf") (isGemaMapName (curmap))) [
    return 1;
  ][
    return 0;
  ]

]

//
// Event handler that is called when the player uses /newmap.
//
checkinit onNewMap [
  setGemaModeActive 0;
]

//
// Event handler that is called when the game is closed.
//
checkinit onQuit [
  setGemaModeActive 0;
]
