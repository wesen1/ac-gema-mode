//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Timestamp formatter module for ac-gema-mode
//
// Provides functions to generate human readable timestamp date and time strings from timestamp arrays.
//
// Requires:
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//

persistidents 0;


//
// Date format for the generated timestamp date strings
//
// Available format specifiers are:
// %Y = Year number (4 digits)
// %m = Month number (2 digits)
// %d = Day number (2 digits)
//
// @var string $dateFormat
//
const dateFormat "%Y-%m-%d";

//
// Time format for the generated timestamp time strings
//
// Available format specifiers are:
// %H = Hours (2 digits)
// %i = Minutes (2 digits)
// %s = Seconds (2 digits)
//
// @var string $timeFormat
//
const timeFormat "%H:%i:%s";


// Public Functions

//
// Generates and returns a date string from a timestamp array.
//
// @param array timestampArray ($arg1) The timestamp array to generate a date string from
//
// @return string The generated date string
//
function formatTimestampDate [

  dateFormatString = (strreplace $dateFormat "%Y" "%1");
  dateFormatString = (strreplace $dateFormatString "%m" "%2");
  dateFormatString = (strreplace $dateFormatString "%d" "%3");

  return (format $dateFormatString (at $arg1 0) (at $arg1 1) (at $arg1 2));

]

//
// Generates and returns a time string from a timestamp array.
//
// @param array timestampArray ($arg1) The timestamp array to generate a time string from
//
// @return string The generated time string
//
function formatTimestampTime [

  timeFormatString = (strreplace $timeFormat "%H" "%1");
  timeFormatString = (strreplace $timeFormatString "%i" "%2");
  timeFormatString = (strreplace $timeFormatString "%s" "%3");

  return (format $timeFormatString (at $arg1 3) (at $arg1 4) (at $arg1 5));

]
