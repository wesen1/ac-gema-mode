

persistidents 0;

const generateMinutesString [

  timeInterval = $arg1;
  hideTimeIntervalPartIfZero = $arg2;
  numberOfZeroPaddingDigits = $arg3;
  timeUnitType = $arg4;
  colorConfig = $arg5;

  timeIntervalMinutes = (getTimeIntervalMinutes $arg1);
  if (|| (= $hideTimeIntervalPartIfZero 0) (> $timeIntervalMinutes 0)) [

    // Add leading 0s
    minutesString = (colorizeTime (l0 $numberOfZeroPaddingDigits $timeIntervalMinutes) $colorConfig);
    minutesString = (concatword $minutesString (colorizeTimeUnit (getMinutesTimeUnit $timeUnitType 1) $colorConfig));

  ][
    minutesString = "";
  ]

  return $minutesString;

]

const generateSecondsString [

  timeInterval = $arg1;
  hideTimeIntervalPartIfZero = $arg2;
  numberOfZeroPaddingDigits = $arg3;
  timeUnitType = $arg4;
  colorConfig = $arg5;

  timeIntervalSeconds = (getTimeIntervalSeconds $arg1);
  if (|| (= $hideTimeIntervalPartIfZero 0) (|| (> (getTimeIntervalMinutes $timeInterval) 0) (> $timeIntervalSeconds 0))) [

    // Add leading 0s
    secondsString = (colorizeTime (l0 $numberOfZeroPaddingDigits $timeIntervalSeconds) $colorConfig);
    secondsString = (concatword $secondsString (colorizeTimeUnit (getSecondsTimeUnit $timeUnitType 1) $colorConfig));

  ][
    secondsString = "";
  ]

  return $secondsString;

]

const generateMillisecondsString [

  timeInterval = $arg1;
  numberOfZeroPaddingDigits = $arg2;
  timeUnitType = $arg3;
  colorConfig = $arg4;

  timeIntervalMilliseconds = (getTimeIntervalMilliseconds $arg1);

  // Milliseconds are always shown even if they are 0 and hideLeadingZeroTimeIntervalParts is true

  // Add leading 0s
  millisecondsString = (colorizeTime (l0 $numberOfZeroPaddingDigits $timeIntervalMilliseconds) $colorConfig);
  millisecondsString = (concatword $millisecondsString (colorizeTimeUnit (getMillisecondsTimeUnit $timeUnitType 1) $colorConfig));

  return $millisecondsString;

]

const generateTotalTimeUnitString [

  timeInterval = $arg1;
  hideLeadingZeroTimeIntervalParts = $arg2;
  timeUnitType = $arg3;
  colorConfig = $arg4;

  if ($hideLeadingZeroTimeIntervalParts) [

    if (> (getTimeIntervalMinutes $timeInterval) 0) [
      totalTimeUnit = (getMinutesTimeUnit $timeUnitType 0);
    ][
      if (> (getTimeIntervalSeconds $timeInterval) 0) [
        totalTimeUnit = (getSecondsTimeUnit $timeUnitType 0);
      ][
        // Minimum time unit is milliseconds, even if the milliseconds part is 0
        totalTimeUnit = (getMillisecondsTimeUnit $timeUnitType 0);
      ]
    ]
  ][
    totalTimeUnit = (getMinutesTimeUnit $timeUnitType 0);
  ]


  return (colorizeTimeUnit $totalTimeUnit $colorConfig);

]
