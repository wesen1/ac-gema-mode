//
// @author wesen
// @copyright 2017 wesen <wesen-ac@web.de>
//

//
// Map record printer module for ac-gema-mode
//
// Handles printing of map records
//
// Requires:
//   * scripts/ac-gema-mode/time-parser.cfg
//   * scripts/ac-gema-mode/map-record.cfg
//

persistidents 0;

//
// Generates a time string in the format <minutes>:<seconds>:<milliseconds> from a time in milliseconds.
//
// @param int $arg1 The time in milliseconds
//
// @return String The time string
//
const generateMapRecordTimeString [

  mapRecordTimeArray = (timeParserConvertMilliSeconds $arg1);

  mapRecordMinutes = (at $mapRecordTimeArray 0);
  mapRecordSeconds = (at $mapRecordTimeArray 1);
  mapRecordMilliSeconds = (at $mapRecordTimeArray 2);

  // Add leading 0s
  mapRecordMinutes = (addLeadingZerosToNumber $mapRecordMinutes 2);
  mapRecordSeconds = (addLeadingZerosToNumber $mapRecordSeconds 2);
  mapRecordMilliSeconds = (addLeadingZerosToNumber $mapRecordMilliSeconds 3);

  // Return the time string
  return (concatword $mapRecordMinutes ":" $mapRecordSeconds "," $mapRecordMilliSeconds);

]

////////////////////////////////////////////////////////////////////////////////////
// Output strings for the console
////////////////////////////////////////////////////////////////////////////////////

//
// Generates and prints the map record output for when the player scores.
//
// @param int $arg1 The record time in milliseconds
//
const printMapRecordScoreString [

  echo (concat (c J) "You scored after" (c F) (generateMapRecordTimeString $arg1) (c J) "minutes");

]

//
// Prints the record status (new record, tied record, no new record).
//
// @param String $arg1 The map name
// @param int $arg2 The new record in milliseconds
//
const printMapRecordStatusString [

  mapRecordTime = (loadMapRecord $arg1);

  if (|| (! $mapRecordTime) (< $arg2 $mapRecordTime)) [
    echo (concat (c 9) "*** New best time ***");
  ][

    if (= $arg2 $mapRecordTime)[
      return (concat (c Q) "Tied your current record");
    ][
      return (concat (c 3) "But you have a better record");
    ]

  ]

]

//
// Shows the best record of a map.
//
// @param String $arg1 The map name
//
const printMapStatistics [

  mapRecordTime = (loadMapRecord $arg1);

  if ($mapRecordTime)[
    echo (concat (c J) "Your best time for this map is" (c 8) (generateMapRecordTimeString $mapRecordTime) (c J) "minutes");
  ][
    echo (concat (c 3) "You don't have any records on this map");
  ]

]


////////////////////////////////////////////////////////////////////////////////////
// Strings for the menus
////////////////////////////////////////////////////////////////////////////////////

//
// Returns a record string for the menus.
//
// @param String $arg1 The map name
//
const getMapRecordMenuString [

  mapRecordTime = (loadMapRecord $arg1);

  if (! $mapRecordTime)[
    return (concat (c 3) "No record");
  ][
    return (concat (c 8) (generateMapRecordTimeString $mapRecordTime));
  ]

]

