//
// @author wesen
// @copyright 2017 wesen <wesen-ac@web.de>
//

//
// Map record printer module for ac-gema-mode
//
// Handles printing of map records
//
// Requires:
//   * scripts/ac-gema-mode/time-parser.cfg
//   * scripts/ac-gema-mode/map-record.cfg
//

persistidents 0;

////////////////////////////////////////////////////////////////////////////////////
// Output strings for the console
////////////////////////////////////////////////////////////////////////////////////

//
// Generates and prints the map record output for when the player scores.
//
// @param int $arg1 The record time in milliseconds
//
const printMapRecordScoreString [

  echo (concat (c $colorRecordInfo) "You scored after" (c $colorRecordTime) (generateTimeString $arg1) (c $colorRecordInfo) "minutes");

]

//
// Prints the record status (new record, tied record, no new record).
//
// @param String $arg1 The map name
// @param int $arg2 The new record in milliseconds
//
const printMapRecordStatusString [

  mapRecordTime = (loadMapRecord $arg1);

  if (|| (! $mapRecordTime) (< $arg2 $mapRecordTime)) [
    echo (concat (c $colorNewRecord) "*** New best time ***");
  ][

    if (= $arg2 $mapRecordTime)[
      echo (concat (c $colorRecordTied) "Tied your current record");
    ][
      echo (concat (c $colorRecordSlower) "But you have a better record");
    ]

  ]

]

//
// Shows the best record of a map.
//
// @param String $arg1 The map name
//
const printMapStatistics [

  mapRecordTime = (loadMapRecord $arg1);

  if ($mapRecordTime)[
    echo (concat (c $colorRecordInfo) "Your best time for this map is" (c $colorRecordTime) (generateTimeString $mapRecordTime) (c $colorRecordInfo) "minutes");
  ][
    echo (concat (c $colorNoRecord) "You don't have any records on this map");
  ]

]


////////////////////////////////////////////////////////////////////////////////////
// Strings for the menus
////////////////////////////////////////////////////////////////////////////////////

//
// Returns a record string for the menus.
//
// @param String $arg1 The map name
//
const getMapRecordMenuString [

  mapRecordTime = (loadMapRecord $arg1);

  if (! $mapRecordTime)[
    return (concat (c $colorNoRecord) "No record");
  ][
    return (concat (c $colorRecordTime) (generateTimeString $mapRecordTime));
  ]

]

