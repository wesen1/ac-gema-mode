//
// @author wesen
// @copyright 2017-2018 wesen <wesen-ac@web.de>
//

//
// Map record printer module for ac-gema-mode
//
// Handles printing of map records
//
// Requires:
//   * scripts/ac-gema-mode/time-parser.cfg
//   * scripts/ac-gema-mode/map-record.cfg
//

checkinit optionShowNameOnScore 0;

persistidents 0;

////////////////////////////////////////////////////////////////////////////////////
// Output strings for the console
////////////////////////////////////////////////////////////////////////////////////

//
// Generates and prints the map record output for when the player scores.
//
// @param int $arg1 The record time in milliseconds
// @param int $arg2 The weapon id of the weapon that the player used to finish the map
//
const printMapRecordScoreString [

  recordMilliseconds = $arg1;
  weaponId = $arg2;

  if ($optionShowNameOnScore) [
    mapRecordString = (concatword (c $colorPlayerName) (player1 name) (c $colorRecordInfo));
  ][
    mapRecordString = (concat (c $colorRecordInfo) "You");
  ]

  mapRecordString = (concat $mapRecordString "scored after" (c $colorRecordTime) (generateTimeString $recordMilliseconds 1) (c $colorRecordInfo) "minutes");

  // TODO: Show weapon only when show weapon is set
  mapRecordString = (concat $mapRecordString "with" (c 1) (getWeaponName $weaponId));

  printGemaNotification $mapRecordString;

]

//
// Prints the record status (new record, tied record, no new record).
// Must be called before the new record was saved.
//
// @param String $arg1 The map name
// @param int $arg2 The new record in milliseconds
// @param int $arg3 The weapon id of the weapon that the player used to finish the map
//
const printMapRecordStatusString [

  status_mapName = $arg1;
  status_recordTime = $arg2;
  status_weaponId = $arg3;

  bestMapRecordTime = (loadMapRecord $status_mapName (getBestMapRecordWeaponId $status_mapName));

  if (|| (! $bestMapRecordTime) (< $status_recordTime $bestMapRecordTime)) [
    // TODO: Show (All weapons) when show weapon is activated?
    printGemaNotification (concat (c $colorNewRecord) "*** New best time ***");
  ][

    // TODO: If show weapon notifications is disabled
    if (= $status_recordTime $bestMapRecordTime)[
      printGemaNotification (concat (c $colorRecordTied) "Tied your current record");
    ][
      printGemaNotification (concat (c $colorRecordSlower) "But you have a better record");
    ]

    // TODO: If show weapon notifications is enabled
    status_mapRecordTime = (loadMapRecord $status_mapName $status_weaponId);
    status_mapRecordWeapon = (getWeaponName $status_weaponId);

    if (|| (! $status_mapRecordTime) (< $status_recordTime $status_mapRecordTime)) [
      printGemaNotification (concat (c $colorNewRecord) "New best time with" (c 1) $status_mapRecordWeapon);
    ][
      if (= $status_recordTime $status_mapRecordTime) [
        printGemaNotification (concat (c $colorRecordTied) "Tied your current record with" (c 1) $status_mapRecordWeapon);
      ][
        printGemaNotification (concat (c $colorRecordSlower) "But you have a better record with" (c 1) $status_mapRecordWeapon);
      ]
    ]
  ]
]

//
// Shows the best record of a map and the weapons with which the map was not finished yet.
//
// @param String $arg1 The map name
//
const printMapStatistics [

  mapName = $arg1;
  bestMapRecordWeaponId = (getBestMapRecordWeaponId $mapName);
  bestMapRecordTime = (loadMapRecord $mapName $bestMapRecordWeaponId);

  if ($bestMapRecordTime)[

    // TODO: Show weapon only when the show weapon option is set
    // TODO: Export weapon color
    printGemaNotification (concat (c $colorRecordInfo) "Your best time for this map is" (c $colorRecordTime) (generateTimeString $bestMapRecordTime 1) (c $colorRecordInfo) "minutes" (c $colorRecordInfo) "with" (c 1) (getWeaponName $bestMapRecordWeaponId));

    // TODO: Show missing weapons string only when show weapon system notifications option is set
    missingWeaponsString = (getMissingWeaponsString $mapName);
    if (! (strcmp $missingWeaponsString "")) [
      printGemaNotification (concat (c 3) "Missing weapons :" $missingWeaponsString);
    ]
  ][
    printGemaNotification (concat (c $colorNoRecord) "You don't have any records on this map");
  ]

]

////////////////////////////////////////////////////////////////////////////////////
// Strings for the menus
////////////////////////////////////////////////////////////////////////////////////

//
// Returns a record string for the menus.
//
// @param String $arg1 The map name
// @param int $arg2 The weapon id
//
// @return String The map record menu string
//
const getMapRecordMenuString [

  mapRecordTime = (loadMapRecord $arg1 $arg2);

  if (! $mapRecordTime)[
    return (concat (c $colorNoRecord) "No record");
  ][
    return (concat (c $colorRecordTime) (generateTimeString $mapRecordTime 1));
  ]

]

//
// Returns the menu record string for the best map record.
//
// @param String $arg1 The map name
//
// @return String The map record menu string
//
const getBestMapRecordMenuString [

  mapName = $arg1;
  bestMapRecordWeaponId = (getBestMapRecordWeaponId $mapName);
  bestMapRecordTimeString = (getMapRecordMenuString $mapName $bestMapRecordWeaponId);

  // TODO: Show this only when weapon notificaitons is enabled?
  if (!= $bestMapRecordWeaponId -1) [
    bestMapRecordTimeString = (concatword $bestMapRecordTimeString (c J) " (" (c 1) (getWeaponName $bestMapRecordWeaponId) (c J) ")");
  ]

  return $bestMapRecordTimeString;
]
