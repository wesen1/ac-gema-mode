//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Current countdown mode state module for ac-gema-mode
//
// Provides functions to manage the current countdown mode state.
//
// Requires:
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/time-target-mode/time-target-mode.cfg
//   * scripts/ac-gema-mode/utils/state-manager.cfg
//

persistidents 0;

initializeState "CountdownMode" 0;


// Public Functions

//
// Returns the current countdown mode state.
//
// @return int 1 if the countdown mode is currently enabled, 0 otherwise
//
const getCurrentCountdownModeState [
  return (getState "CountdownMode");
]

//
// Tries to set the current countdown mode state.
// If the countdown mode state was successfully changed an optional success message will be shown.
// Otherwise the reason for not changing the countdown mode state will be shown.
//
// @param int newCountdownModeState ($arg1) The new countdown mode state
// @param string successNotification ($arg2) The notification to show on success (optional)
//
// @return int 1 if the countdown mode state was successfully set, 0 otherwise
//
const tryToSetCurrentCountdownModeState [

  if (= $numargs 2) [
    return (tryToSetState "CountdownMode" $arg1 $arg2);
  ][
    return (tryToSetState "CountdownMode" $arg1);
  ]

]

//
// Returns the reason why the countdown mode state can not be changed to a specified state.
//
// @param int newCountdownModeState ($arg1) The state to which the countdown mode state should be set (0 or 1)
//
// @return string The reason why the countdown mode state can not be changed or an empty string if it can be changed
//
const getCountdownModeStateCanNotBeChangedReason [

  if (= $arg1 1) [
    if (! (isTimeTargetModeActive)) [
      return $messageTimeTargetModeNotActive;
    ]
  ]

]


// Custom Events

//
// Event handler that is called when the countdown mode state changes from "disabled" to "enabled".
//
checkinit onCountdownModeEnabled [
]

//
// Event handler that is called when the countdown mode state changes from "enabled" to "disabled".
//
checkinit onCountdownModeDisabled [
]
