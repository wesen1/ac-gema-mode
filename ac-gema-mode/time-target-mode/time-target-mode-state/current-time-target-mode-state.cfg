//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Current time target mode state module for ac-gema-mode
//
// Provides functions to manage the current time target mode state.
//
// Requires:
//   * scripts/ac-gema-mode/gema-mode-state/environment-checker.cfg
//   * scripts/ac-gema-mode/utils/state-manager.cfg
//

persistidents 0;

initializeState "TimeTargetMode" 0;


// Public Functions

//
// Returns the current time target mode state.
//
// @return int 1 if the time target mode is currently enabled, 0 otherwise
//
const getCurrentTimeTargetModeState [
  return (getState "TimeTargetMode");
]


//
// Tries to set the current time target mode state.
// If the time target mode state was successfully changed an optional success message will be shown.
// Otherwise the reason for not changing the time target mode state will be shown.
//
// @param int newTimeTargetModeState ($arg1) The new time target mode state
// @param string successNotification ($arg2) The notification to show on success (optional)
//
// @return int 1 if the time target mode state was successfully set, 0 otherwise
//
const tryToSetTimeTargetModeState [

  if (= $numargs 2) [
    return (tryToSetState "TimeTargetMode" $arg1 $arg2);
  ][
    return (tryToSetState "TimeTargetMode" $arg1);
  ]

]

//
// Returns the reason why the time target mode state can not be changed to a specified state.
//
// @param int newTimeTargetModeState ($arg1) The state to which the time target mode state should be set (0 or 1)
//
// @return string The reason why the time target mode state can not be changed or an empty string if it can be changed
//
const getTimeTargetModeStateCanNotBeChangedReason [

  if (= $arg1 1) [
    if (! (isGemaModeActive)) [
      return $messageGemaModeNotActive;
    ][
      if (< (getTimeTargetTotalMilliseconds) 0) [
        return $messageInvalidTimeTarget;
      ]
    ]
  ]

]


// Custom Events

//
// Event handler that is called when the time target mode state changes from "disabled" to "enabled".
//
checkinit onTimeTargetModeEnabled [
]

//
// Event handler that is called when the time target mode state changes from "enabled" to "disabled".
//
checkinit onTimeTargetModeDisabled [
]
