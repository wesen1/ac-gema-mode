//
// @author wesen
// @copyright 2019-2020 wesen <wesen-ac@web.de>
//

//
// Time target auto updater module for ac-gema-mode
//
// Handles automatic updating of the time target.
//
// Requires:
//   * scripts/ac-gema-mode/custom-declarations.cfg
//   * scripts/ac-gema-mode/map-record/storage/best-map-record-cache.cfg
//   * scripts/ac-gema-mode/map-record/storage/map-record-loader.cfg
//   * scripts/ac-gema-mode/map-record/storage/map-record-storage.cfg
//   * scripts/ac-gema-mode/notification-printer.cfg
//   * scripts/ac-gema-mode/score-attempt/current-score-attempt.cfg
//   * scripts/ac-gema-mode/score-attempt/score-attempt.cfg
//   * scripts/ac-gema-mode/score-attempt/score-attempt-array.cfg
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/time-target/time-target-mode.cfg
//   * scripts/ac-gema-mode/unpersist/declarations.cfg
//   * scripts/ac-gema-mode/util/weapon-name-fetcher.cfg
//   * scripts/ac-gema-mode/util/value-persistor.cfg
//

// Options

//
// Option to auto reset the time target to 0 on quit (Default: 1)
//
// @var int $optionResetTimeTargetOnQuit
//
option optionResetTimeTargetOnQuit 1;

//
// Option to auto set the time target (Default: 0).
//
// 0: Disabled
// 1: Best Map Record
// 2: Best Primary Weapon Record
// 3: Best Score Attempt Weapon Record
//
// @var int $optionAutoSetTimeTargetMode
//
option optionAutoSetTimeTargetMode 0;

persistidents 0;


// Global Variables

//
// A delayed time target update that was detected but should not be applied instantly
//
// This is used to prevent saving of new map records from changing the time target
// before its comparison to the score attempt map record time is shown in the console
//
// @var int $delayedTimeTargetUpdate
//
cache delayedTimeTargetUpdate -1;


// Public Functions

//
// Initializes the auto time target with the configured auto set mode.
//
function initializeAutoTimeTarget [

  if (isTimeTargetModeActive) [
    updateBestMapRecordTimeTargetIfRequired;
    updateBestWeaponRecordTimeTargetIfRequired;

    if (isScoreAttemptActive) [
      updateBestScoreWeaponRecordTimeTargetIfRequired (getScoreAttemptWeaponId (getCurrentScoreAttempt));
    ]
  ]

]

//
// Delays a time target update.
// The update will be applied on the next "applyDelayedTimeTargetUpdateIfRequired" call.
//
// @param int delayedTimeTargetUpdate ($arg1) The delayed time target update
//
function delayTimeTargetUpdate [
  delayedTimeTargetUpdate = $arg1;
]

//
// Applies a delayed time target update if there is one.
//
function applyDelayedTimeTargetUpdateIfRequired [

  if (!= $delayedTimeTargetUpdate -1) [
    updateTimeTarget $delayedTimeTargetUpdate;
    delayedTimeTargetUpdate = -1;
  ]

]

//
// Sets $optionAutoSetTimeTargetMode and initializes the time target with the new auto set mode.
//
// @param int newAutoSetMode ($arg1) The new value for $optionAutoSetTimeTargetMode
//
function setOptionAutoSetTimeTargetMode [

  if (!= $arg1 $optionAutoSetTimeTargetMode) [
    setPersistentValue optionAutoSetTimeTargetMode $arg1;
    if (!= $optionAutoSetTimeTargetMode 0) [
      initializeAutoTimeTarget;
    ]
  ]

]


// Private Functions

//
// Changes the current time target.
//
// @param int newTimeTarget ($arg1) The new time target in milliseconds
//
function updateTimeTarget [

  newTimeTarget = $arg1;
  if (!= (getCurrentTimeTargetTotalMilliseconds) $newTimeTarget) [
    setCurrentTimeTargetTotalMilliseconds $newTimeTarget;
  ]

]

//
// Sets the time target to the best map record if $optionAutoSetTimeTargetMode is 1.
//
// @param int delayTimeTargetUpdate ($arg1) 1 if the time target update should be delayed, 0 otherwise
//
function updateBestMapRecordTimeTargetIfRequired [

  if (= $numargs 1) [
    delayBestMapRecordTimeTargetUpdate = $arg1;
  ][
    delayBestMapRecordTimeTargetUpdate = 0;
  ]

  if (= $optionAutoSetTimeTargetMode 1) [

    bestMapRecord = (getBestMapRecord (curmap) $curmaprevision);
    if (strcmp $bestMapRecord "") [
      updatedTimeTarget = 0;
    ][
      updatedTimeTarget = (getMapRecordScoreTime $bestMapRecord);
    ]

    if ($delayBestMapRecordTimeTargetUpdate) [
      delayTimeTargetUpdate $updatedTimeTarget;
    ][
      updateTimeTarget $updatedTimeTarget;
    ]

  ]
]

//
// Sets the time target to the best primary weapon record if $optionAutoSetTimeTarget is 2.
//
function updateBestWeaponRecordTimeTargetIfRequired [

  if (= $optionAutoSetTimeTargetMode 2) [

    bestWeaponMapRecord = (loadWeaponMapRecord (curmap) (player1 primary) $curmaprevision);
    if (strcmp $bestWeaponMapRecord "") [
      updatedTimeTarget = 0;
    ][
      updatedTimeTarget = (getMapRecordScoreTime $bestWeaponMapRecord);
    ]

    updateTimeTarget $updatedTimeTarget;
  ]

]

//
// Sets the time target to the best record for the current score weapon if $optionAutoSetTimeTarget is 3.
//
// @param int currentScoreWeaponId ($arg1) The id of the current score weapon
//
function updateBestScoreWeaponRecordTimeTargetIfRequired [

  if (= $optionAutoSetTimeTargetMode 3) [
    currentScoreWeaponId = $arg1;
    printGemaNotification (format $messageAutoSetTimeTargetToScoreWeaponRecord (getWeaponName $currentScoreWeaponId));

    bestScoreWeaponMapRecord = (loadWeaponMapRecord (curmap) $currentScoreWeaponId $curmaprevision);
    if (strcmp $bestScoreWeaponMapRecord "") [
      updatedTimeTarget = 0;
    ][
      updatedTimeTarget = (getMapRecordScoreTime $bestScoreWeaponMapRecord);
    ]

    updateTimeTarget $updatedTimeTarget;
  ]

]


// Event Handlers

//
// Event handler that is called when a new map starts.
//
eventlistener mapstartalways [
  if (isTimeTargetModeActive) [
    updateBestMapRecordTimeTargetIfRequired;
  ]
]

//
// Event handler that is called when the time target mode state changes from "disabled" to "enabled".
//
eventlistener onTimeTargetModeEnabled [
  initializeAutoTimeTarget;
]

//
// Event handler that is called after the current score attempt was started.
//
eventlistener onCurrentScoreAttemptStarted [
  if (isTimeTargetModeActive) [
    updateBestScoreWeaponRecordTimeTargetIfRequired $KNIFE;
    updateBestWeaponRecordTimeTargetIfRequired;
  ]
]

//
// Event handler that is called after the score weapon of the current score attempt was changed.
//
// @param int newScoreWeaponId ($arg1) The new score weapon id
//
eventlistener onCurrentScoreAttemptScoreWeaponChanged [
  if (isTimeTargetModeActive) [
    updateBestScoreWeaponRecordTimeTargetIfRequired $arg1;
  ]
]

//
// Event handler that is called after a map record was saved.
//
// @param array mapRecord ($arg1) The map record
//
eventlistener onMapRecordSaved [
  if (isTimeTargetModeActive) [
    updateBestMapRecordTimeTargetIfRequired 1;
  ]
]

//
// Event handler that is called after a weapon map record was deleted.
//
// @param array mapRecord ($arg1) The map record
//
eventlistener onWeaponRecordDeleted [

  if (isTimeTargetModeActive) [
    updateBestMapRecordTimeTargetIfRequired;
    updateBestWeaponRecordTimeTargetIfRequired;
    updateBestScoreWeaponRecordTimeTargetIfRequired;
  ]

]

//
// Event handler that is called after all records for a map in a specific revision were deleted.
//
// @param string mapName ($arg1) The map name
// @param int mapRevision ($arg2) The map revision
//
eventlistener onAllRecordsOfMapInRevisionDeleted [

  if (isTimeTargetModeActive) [
    updateBestMapRecordTimeTargetIfRequired;
    updateBestWeaponRecordTimeTargetIfRequired;
    updateBestScoreWeaponRecordTimeTargetIfRequired;
  ]

]

//
// Event handler that is called when the game is closed.
//
eventlistener onQuit [
  if ($optionResetTimeTargetOnQuit) [
    setCurrentTimeTargetTotalMilliseconds 0;
  ]
]
