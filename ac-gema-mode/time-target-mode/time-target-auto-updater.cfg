//
// @author wesen
// @copyright 2019 wesen <wesen-ac@web.de>
//

//
// Handles automatic updating of the time target.
//
// Requires:
//   * scripts/ac-gema-mode/strings.cfg
//   * scripts/ac-gema-mode/notification-printer.cfg
//   * scripts/ac-gema-mode/map-record/storage/best-map-record-loader.cfg
//   * scripts/ac-gema-mode/map-record/storage/map-record-storage.cfg
//   * scripts/ac-gema-mode/score-attempt/score-weapon-updater.cfg
//   * scripts/ac-gema-mode/time-target/time-target-mode.cfg
//   * scripts/ac-gema-mode/util/weapon-name-fetcher.cfg
//

// Options

//
// Option to auto set the time target
//
// 0: Disabled
// 1: Best Map Record
// 2: Best Primary Weapon Record
// 3: Score Attempt Weapon
//
// @var int $optionAutoSetTimeTargetMode
//
check2init optionAutoSetTimeTargetMode 0;


persistidents 0;

// Public Functions

//
// Initializes the auto time target with the configured auto set mode.
//
const initializeAutoTimeTarget [
  updateBestMapRecordTimeTarget;
  updateBestWeaponRecordTimeTarget;
]

//
// Sets $optionAutoSetTimeTargetMode and initializes the time target with the new auto set mode.
//
// @param $arg1 The new $optionAutoSetTimeTargetMode state
//
const setOptionAutoSetTimeTargetMode [
  optionAutoSetTimeTargetMode = $arg1;
  initializeAutoTimeTarget;
]


// Private Functions

//
// Updates the time target if the new time target is different from the current time target.
//
// @param int $arg1 The new time target
//
const updateTimeTarget [

  newTimeTarget = $arg1;
  if (!= $timeTarget $newTimeTarget) [
    setTimeTarget $newTimeTarget;
  ]

]

//
// Sets the time target to the best map record if $optionAutoSetTimeTargetMode is 1.
//
const updateBestMapRecordTimeTarget [
  if (= $optionAutoSetTimeTargetMode 1) [
    updateTimeTarget (getMapRecordScoreTime (getBestMapRecord (curmap)));
  ]
]

//
// Sets the time target to the best primary weapon record if $optionAutoSetTimeTarget is 2.
//
const updateBestWeaponRecordTimeTarget [
  if (= $optionAutoSetTimeTargetMode 2) [
    updateTimeTarget (getMapRecordScoreTime (loadMapRecord (curmap) (player1 primary)));
  ]
]

//
// Sets the time target to the best current score weapon record if $optionAutoSetTimeTarget is 3.
//
// @param int $arg1 The id of the current score weapon
//
const updateBestScoreWeaponRecordTimeTarget [

  if (= $optionAutoSetTimeTargetMode 3) [
    currentScoreWeaponId = $arg1;
    printGemaNotification (format $messageAutoSetTimeTargetToScoreWeaponRecord (getWeaponName $currentScoreWeaponId));
    updateTimeTarget (getMapRecordScoreTime (loadMapRecord (curmap) $currentScoreWeaponId));
  ]

]


// Event Handlers

//
// Event handler that is called when a new map starts.
//
checkinit mapstartalways [
  if (isTimeTargetModeActive) [
    updateBestMapRecordTimeTarget;
  ]
]

//
// Event handler that is called when the time target mode state changes to enabled.
//
checkinit onEnableTimeTargetMode [
  initializeAutoTimeTarget;
]

//
// Event handler that is called after a score attempt was started.
//
checkinit onScoreAttemptStartAfter [
  if (isTimeTargetModeActive) [
    updateBestScoreWeaponRecordTimeTarget $KNIFE;
    updateBestWeaponRecordTimeTarget;
  ]
]

//
// Event handler that is called when the score weapon is updated.
//
// @param int $arg1 The new score weapon id
//
checkinit onScoreWeaponUpdate [
  if (isTimeTargetModeActive) [
    updateBestScoreWeaponRecordTimeTarget $arg1;
  ]
]

//
// Event handler that is called after a map record was saved.
//
// @param array $arg1 The map record
//
checkinit onMapRecordSavedAfter [
  if (isTimeTargetModeActive) [
    updateBestMapRecordTimeTarget;
  ]
]
