//
// @author wesen
// @copyright 2017 wesen <wesen-ac@web.de>
//

//
// Map record module for ac-gema-mode
//
// Handles saving, loading and deleting of map records.
//

persistidents 0;


////////////////////////////////////////////////////////////////////////////////////
// Read and write map records
////////////////////////////////////////////////////////////////////////////////////

//
// Saves a map record if it is better than the previous one.
//
// @param String $arg1 The map name
// @param int $arg2 The record time in milliseconds
//
const saveMapRecord [

  currentMapRecord = (loadMapRecord $arg1);

  // If there is no record for this map or the record is worse than the new one 
  if (|| (! $currentMapRecord) (< $arg2 $currentMapRecord)) [

    // save map records in saved.cfg
    persistidents 1;

    // Assigns the record time to a variable with the name that is stored in $arg1
    $arg1 = $arg2;

    persistidents 0;
  ]

]

//
// Loads a record.
//
// @param String $arg1 The map name
//
const loadMapRecord [

  if (isIdent $arg1)[
    return ($arg1);
  ][
    return 0;
  ]

]

//
// Deletes a record.
//
// @param String $arg1 The map name
//
const deleteMapRecord [

  if (isIdent $arg1)[
    delalias $arg1;
    echo (concat (c J) "Record successfully deleted");
  ]

]

