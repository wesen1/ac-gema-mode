//
// @author wesen
// @copyright 2017-2019 wesen <wesen-ac@web.de>
//

//
// Time parser module for ac-gema-mode
//
// Provides methods to convert milliseconds to an array in the format [ <minutes> <seconds> <milliseconds> ] and to generate time strings.
//

persistidents 0;

//
// Converts milliseconds to an array in the format [ <minutes> <seconds> <milliseconds> ].
//
// @param int $arg1 The time in milliseconds
//
// @return array The minutes, seconds and milliseconds part
//
const timeParserConvertMilliseconds [

  timeParserMilliseconds = (mod $arg1 1000);
  timeParserTotalSeconds = (div $arg1 1000);

  timeParserMinutes = (div $timeParserTotalSeconds 60);
  timeParserSeconds = (mod $timeParserTotalSeconds 60);

  return (concat $timeParserMinutes $timeParserSeconds $timeParserMilliseconds);

]

//
// Generates and returns a time string in the format <minutes>:<seconds>:<milliseconds> from a time in milliseconds.
//
// @param int $arg1 The time in milliseconds
// @param int $arg2 If 1 the time string will contain the milliseconds part
//
// @return String The time string
//
const generateTimeString [

  timeArray = (timeParserConvertMilliseconds $arg1);

  // Add leading 0s
  minutesString = (l0 2 (at $timeArray 0));
  secondsString = (l0 2 (at $timeArray 1));

  timeString = (concatword $minutesString ":" $secondsString);

  if ($arg2) [
    millisecondsString = (l0 3 (at $timeArray 2));
    timeString = (concatword $timeString "," $millisecondsString);
  ]

  return $timeString;

]
