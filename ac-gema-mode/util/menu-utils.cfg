//
// @author wesen
// @copyright 2020 wesen <wesen-ac@web.de>
//

//
// Menu utils module for ac-gema-mode
//
// Provides various menu related util functions.
//

// Options

//
// The font type that should be used to format menus
// If -1 the font type will be dynamically read from $__fontsetting.
// If >= 0 the font type will be fixed to the $optionMenuFontType value
//
// @var int $optionMenuFontType
//
check2init optionMenuFontType -1;

persistidents 0;


// Global Variables

//
// The last font type that the menu pad tabs were calculated for
//
// @var int $lastCalculatedMenuPadTabsFontType
//
checkinit lastCalculatedMenuPadTabsFontType -1;

//
// The list of menu pad tab calculating functions
// These functions will be called every time the calculateMenuPadTabs function recalcuates the menu pad tabs
//
// @var string[] $menuPadTabCalculators
//
checkinit menuPadTabCalculators [];


// Public Functions

//
// Reloads a menu and selects a specified line in the menu.
// This can be used to properly update checkboxes that the user is not allowed to activate or to
// reset text input field values when invalid input text is inserted.
//
// @param string menuName ($arg1) The name of the menu to refresh
// @param int lineNumber ($arg2) The line number to select after the refresh
//
const refreshMenu [
  menuName = $arg1;
  selectedLineNumber = $arg2;

  closemenu $menuName;
  showmenu $menuName;
  menuselection $menuName $selectedLineNumber;
]

//
// Calculates the menu pad tabs using the $menuPadTabCalculators if the given font type differs from the $lastCalculatedMenuPadTabsFontType.
//
// @param int fontType ($arg1) The font type to calculate the menu pad tabs for
//
const calculateMenuPadTabs [

  fontType = $arg1;
  if (!= $lastCalculatedMenuPadTabsFontType $fontType) [
    looplist $menuPadTabCalculators menuPadTabCalculator [
      $menuPadTabCalculator $fontType;
    ]

    lastCalculatedMenuPadTabsFontType = $fontType;
  ]

]

//
// Adds a menu pad tab calculating function.
//
// @param string menuPadTabCalculator ($arg1) The name of the menu pad tab calculating function to add
//
const addMenuPadTabCalculator [
  add2list menuPadTabCalculators $arg1;
]


//
// Returns the font type that should be used for formatting menus.
//
// @return int The menu font type
//
const getMenuFontType [

  if (= $optionMenuFontType -1) [
    return $__fontsetting;
  ][
    return $optionMenuFontType;
  ]

]

//
// Returns the value names for the possible values of the menu font type option.
//
// @return string[] The value names for the possible values of the menu font type option
//
const getAvailableMenuFontTypes [
  return (concat "auto" (getAvailableFontTypes));
]
