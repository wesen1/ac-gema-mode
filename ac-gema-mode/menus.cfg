//
// @author wesen
// @copyright 2017 wesen <wesen-ac@web.de>
//

//
// Requires:
//   * scripts/ac-gema-mode.cfg
//   * scripts/ac-gema-mode/map-record.cfg
//   * scripts/ac-gema-mode/map-record-printer.cfg
//   * scripts/ac-gema-mode/run-system.cfg
//

persistidents 0;

const menuGemaModeTitle (concatword (c $colorGemaModeMenuTitle) "Gema Mode" (c $colorMenuDefault));
const menuDeleteRecordTitle (concatword $menuGemaModeTitle (c $colorMenuDefault) " - " (c $colorDeleteRecordTitle) "Delete record" (c $colorMenuDefault));
const menuOptionsTitle (concatword $menuGemaModeTitle (c $colorMenuDefault) " - " (c $colorOptionsTitle) "Options" (c $colorMenuDefault));

//
// Main menu
//
newmenu $menuGemaModeTitle
menuitemcheckbox (concat (c $colorEnableGemaMode) "Enable gema mode") [ isGemaModeActive ][ setGemaModeActive $arg1 ]
menuitem
menuitemvar [ concat (c $colorMapNameTitle) "Map:" (c $colorMapName) (curmap) ]
menuitemvar [ concat (c $colorBestTimeTitle) "Best Time:" (getMapRecordMenuString (curmap)) ][ showDeleteRecordMenu (curmap) ]
menuitem
menuitem (concat (c $colorOptionsTitle) "Options") [ showmenu $menuOptionsTitle ]
menuitem
menuitem (concat (c $colorClose) "Close") [ closemenu ]

//
// Shows the delete record menu if there is a record for the map.
//
// @param String $arg1 The map name
//
const showDeleteRecordMenu [

  currentMapRecord = (loadMapRecord $arg1);

  if ($currentMapRecord) [
    showmenu $menuDeleteRecordTitle;
  ]

]

//
// Delete record menu
//
newmenu $menuDeleteRecordTitle
menuitemvar [ concatword (c $colorDeleteRecordText) "Delete record for " (c $colorMapName) (curmap) (c $colorDeleteRecordText) "?" ]
menuitem
menuitem (concat (c $colorDeleteRecordOptionYes) "Yes, delete the record") [ deleteMapRecord (curmap); showmenu $menuGemaModeTitle ]
menuitem (concat (c $colorDeleteRecordOptionNo) "No, don't delete the record") [ showmenu $menuGemaModeTitle ]
menuinitselection 3; // Initially select "No"

//
// Options menu
//
newmenu $menuOptionsTitle
menuitemcheckbox (concat (c $colorGeneralOption) "Auto activate gema mode") [ $optionAutoActivateGemaMode ][ optionAutoActivateGemaMode = $arg1 ]
menuitemcheckbox (concat (c $colorGeneralOption) "Show notifications") [ $optionShowNotifications ] [ optionShowNotifications = $arg1 ]
menuitem
menuitem (concat (c $colorClose) "Back") [ showmenu $menuGemaModeTitle ]

