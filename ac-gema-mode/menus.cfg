//
// @author wesen
// @copyright 2017-2018 wesen <wesen-ac@web.de>
//

//
// Requires:
//   * scripts/ac-gema-mode.cfg
//   * scripts/ac-gema-mode/map-record.cfg
//   * scripts/ac-gema-mode/map-record-printer.cfg
//   * scripts/ac-gema-mode/run-system.cfg
//

persistidents 0;

//
// The weapon id for the delete record dialog
//
// @var int $deleteRecordWeaponId
//
checkinit deleteRecordWeaponId 0;

const menuGemaModeTitle (concatword (c $colorGemaModeMenuTitle) "Gema Mode" (c $colorMenuDefault));
const gemaModeSubMenuPrefix (concatword $menuGemaModeTitle (c $colorMenuDefault) " - ");

const menuShowRecordsTitle (concatword $gemaModeSubMenuPrefix (c 4) "Map records");
const menuDeleteRecordTitle (concatword $gemaModeSubMenuPrefix (c $colorDeleteRecordTitle) "Delete record" (c $colorMenuDefault));
const menuTimeTargetTitle (concatword $gemaModeSubMenuPrefix (c $colorTimeTargetTitle) "Time target" (c $colorMenuDefault));
const menuOptionsTitle (concatword $gemaModeSubMenuPrefix (c $colorOptionsTitle) "Options" (c $colorMenuDefault));

//
// Main menu
//
newmenu $menuGemaModeTitle
menuitemcheckbox (concat (c $colorEnableGemaMode) "Enable gema mode") [ isGemaModeActive ][ setGemaModeActive $arg1 ]
menuitem
menuitemvar [ concat (c $colorRecordInfoTitle) "Map:" (c $colorMapName) (curmap) ]
menuitemvar [ concat (c $colorRecordInfoTitle) "Best Time:" (getBestMapRecordMenuString (curmap)) ][ showmenu $menuShowRecordsTitle ]
menuitemvar [ concat (c $colorRecordInfoTitle) "Time Target:" (c $colorTimeTargetTime) (generateTimeString $timeTarget 1) ][ showmenu $menuTimeTargetTitle ]
menuitem
menuitem (concat (c $colorOptionsTitle) "Options") [ showmenu $menuOptionsTitle ]
menuitem
menuitem (concat (c $colorClose) "Close") [ closemenu ]

//
// Records menu
//
newmenu $menuShowRecordsTitle
menuitemvar [ concat (c $colorRecordInfoTitle) "Map:" (c $colorMapName) (curmap) ]
menuitem
menuitemvar [ getRecordMenuItemText (curmap) $AR ] [ showDeleteRecordMenu (curmap) $AR ]
menuitemvar [ getRecordMenuItemText (curmap) $SMG ] [ showDeleteRecordMenu (curmap) $SMG ]
menuitemvar [ getRecordMenuItemText (curmap) $SNIPER ] [ showDeleteRecordMenu (curmap) $SNIPER ]
menuitemvar [ getRecordMenuItemText (curmap) $SHOTGUN ] [ showDeleteRecordMenu (curmap) $SHOTGUN ]
menuitemvar [ getRecordMenuItemText (curmap) $CARBINE ] [ showDeleteRecordMenu (curmap) $CARBINE ]
menuitemvar [ getRecordMenuItemText (curmap) $PISTOL ] [ showDeleteRecordMenu (curmap) $PISTOL ]
menuitemvar [ getRecordMenuItemText (curmap) $KNIFE ] [ showDeleteRecordMenu (curmap) $KNIFE ]
menuitem
menuitem (concat (c $colorClose) "Back") [ showmenu $menuGemaModeTitle ]

//
// Returns the menu item text for a record with a specific weapon.
//
// @param String $arg1 The map name
// @param int $arg2 The weapon id
//
// @return String The menu item text for the record
//
const getRecordMenuItemText [

  mapName = $arg1;
  weaponId = $arg2;

  return (concatword (c $colorWeapon) (getWeaponName $weaponId) ": " (getMapRecordMenuString $mapName $weaponId));

]

//
// Shows the delete record menu if there is a record for the map with the specific weapon.
//
// @param String $arg1 The map name
// @param int $arg2 The weapon id
//
const showDeleteRecordMenu [

  mapName = $arg1;
  weaponId = $arg2;

  currentMapRecord = (loadMapRecord $mapName $weaponId);

  if ($currentMapRecord) [
    deleteRecordWeaponId = $weaponId;
    showmenu $menuDeleteRecordTitle;
  ][
    // Prevents the gema mode menu from closing
    -1;
  ]

]

//
// Delete record menu
//
newmenu $menuDeleteRecordTitle
menuitemvar [ concatword (c $colorDeleteRecordText) "Delete record for " (c $colorMapName) (curmap) (c $colorDeleteRecordText) " with " (c $colorWeapon) (getWeaponName $deleteRecordWeaponId) (c $colorDeleteRecordText) "?" ]
menuitem
menuitem (concat (c $colorDeleteRecordOptionYes) "Yes, delete the record") [ deleteMapRecord (curmap) $deleteRecordWeaponId; showmenu $menuGemaModeTitle ]
menuitem (concat (c $colorDeleteRecordOptionNo) "No, don't delete the record") [ showmenu $menuGemaModeTitle ]

// Initially select "No"
menuinitselection 3;


//
// Options menu
//
newmenu $menuOptionsTitle
menuitem (concat (c $colorOptionsTitle) "General:") [ -1 ]
menuitemcheckbox (concat (c $colorGeneralOption) "Auto activate gema mode") [ $optionAutoActivateGemaMode ][ optionAutoActivateGemaMode = $arg1 ]
menuitemcheckbox (concat (c $colorGeneralOption) "Show notifications") [ $optionShowNotifications ] [ optionShowNotifications = $arg1 ]
menuitemcheckbox (concat (c $colorGeneralOption) "Show player name when scoring") [ $optionShowNameOnScore ] [ optionShowNameOnScore = $arg1 ]
menuitem

menuitem (concat (c $colorOptionsTitle) "Time target:") [ -1 ]
menuitemcheckbox (concat (c $colorTimeTargetOption) "Auto set time target to map record") [ $optionAutoSetTimeTargetToMapRecord ][ optionAutoSetTimeTargetToMapRecord = $arg1 ]
// TODO: Add menu item to select the map record source (knife, pistol, primary or best)
// TODO: Could be a sub menu or a slider

menuitemcheckbox (concat (c $colorTimeTargetOption) "Auto set time target to 0 on quit") [ $optionResetTimeTargetOnQuit ][ optionResetTimeTargetOnQuit = $arg1 ]
menuitem

menuitem (concat (c $colorOptionsTitle) "Weapons:") [ -1 ]
menuitemcheckbox (concat (c $colorWeapon) "Show score weapon") [ $optionShowScoreWeapon ] [ optionShowScoreWeapon = $arg1 ]
menuitemcheckbox (concat (c $colorWeapon) "Show Knife/Pistol mode abort messages") [ $optionShowSecondaryWeaponModeAbortedMessages ][ optionShowSecondaryWeaponModeAbortedMessages = $arg1 ]

// TODO: Add the options
//menuitemcheckbox (concat (c J) "Show weapon system notifications" [ optionShowWeaponSystemNotifications = $arg1 ]

menuitem
menuitem (concat (c $colorClose) "Back") [ showmenu $menuGemaModeTitle ]

//
// Time target menu
//
newmenu $menuTimeTargetTitle
menuitemcheckbox (concat (c $colorTimeTargetEnable) "Enable Time Target Mode") [ (isTimeTargetModeActive) ][ setTimeTargetModeActive $arg1 ]
menuitemcheckbox (concat (c $colorCountDownEnable) "Show countdown") [ (isCountDownModeActive) ][ setCountDownModeActive $arg1 ]
menuitem
menuitemtextinput (concat (c $colorTimeTargetTime) "Minutes") [ (timeTargetGetMinutes) ][ timeTargetSetMinutes $arg1 ]
menuitemtextinput (concat (c $colorTimeTargetTime) "Seconds") [ (timeTargetGetSeconds) ][ timeTargetSetSeconds $arg1 ]
menuitemtextinput (concat (c $colorTimeTargetTime) "Milliseconds  ") [ (timeTargetGetMilliseconds) ][ timeTargetSetMilliseconds $arg1 ]
menuitem
menuitem (concat (c $colorClose) "Back") [ showmenu $menuGemaModeTitle ]
