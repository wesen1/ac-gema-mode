//
// @author wesen
// @copyright 2017-2018 wesen <wesen-ac@web.de>
//

//
// Requires:
//   * scripts/ac-gema-mode.cfg
//   * scripts/ac-gema-mode/map-record.cfg
//   * scripts/ac-gema-mode/map-record-printer.cfg
//   * scripts/ac-gema-mode/run-system.cfg
//

persistidents 0;

const menuGemaModeTitle (concatword (c $colorGemaModeMenuTitle) "Gema Mode" (c $colorMenuDefault));
const gemaModeSubMenuPrefix (concatword $menuGemaModeTitle (c $colorMenuDefault) " - ");

const menuDeleteRecordTitle (concatword $gemaModeSubMenuPrefix (c $colorDeleteRecordTitle) "Delete record" (c $colorMenuDefault));
const menuTimeTargetTitle (concatword $gemaModeSubMenuPrefix (c $colorTimeTargetTitle) "Time target" (c $colorMenuDefault));
const menuOptionsTitle (concatword $gemaModeSubMenuPrefix (c $colorOptionsTitle) "Options" (c $colorMenuDefault));

//
// Main menu
//
newmenu $menuGemaModeTitle
menuitemcheckbox (concat (c $colorEnableGemaMode) "Enable gema mode") [ isGemaModeActive ][ setGemaModeActive $arg1 ]
menuitem
menuitemvar [ concat (c $colorRecordInfoTitle) "Map:" (c $colorMapName) (curmap) ]
menuitemvar [ concat (c $colorRecordInfoTitle) "Best Time:" (getMapRecordMenuString (curmap)) ][ showDeleteRecordMenu (curmap) ]
menuitemvar [ concat (c $colorRecordInfoTitle) "Time Target:" (c $colorTimeTargetTime) (generateTimeString $timeTarget 1) ][ showmenu $menuTimeTargetTitle ]
menuitem
menuitem (concat (c $colorOptionsTitle) "Options") [ showmenu $menuOptionsTitle ]
menuitem
menuitem (concat (c $colorClose) "Close") [ closemenu ]

//
// Shows the delete record menu if there is a record for the map.
//
// @param String $arg1 The map name
//
const showDeleteRecordMenu [

  currentMapRecord = (loadMapRecord $arg1);

  if ($currentMapRecord) [
    showmenu $menuDeleteRecordTitle;
  ][
    // Prevents the gema mode menu from closing
    -1;
  ]

]

//
// Delete record menu
//
newmenu $menuDeleteRecordTitle
menuitemvar [ concatword (c $colorDeleteRecordText) "Delete record for " (c $colorMapName) (curmap) (c $colorDeleteRecordText) "?" ]
menuitem
menuitem (concat (c $colorDeleteRecordOptionYes) "Yes, delete the record") [ deleteMapRecord (curmap); showmenu $menuGemaModeTitle ]
menuitem (concat (c $colorDeleteRecordOptionNo) "No, don't delete the record") [ showmenu $menuGemaModeTitle ]
menuinitselection 3; // Initially select "No"

//
// Options menu
//
newmenu $menuOptionsTitle
menuitemcheckbox (concat (c $colorGeneralOption) "Auto activate gema mode") [ $optionAutoActivateGemaMode ][ optionAutoActivateGemaMode = $arg1 ]
menuitemcheckbox (concat (c $colorGeneralOption) "Show notifications") [ $optionShowNotifications ] [ optionShowNotifications = $arg1 ]
menuitemcheckbox (concat (c $colorGeneralOption) "Show player name when scoring") [ $optionShowNameOnScore ] [ optionShowNameOnScore = $arg1 ]
menuitem
menuitem (concat (c $colorClose) "Back") [ showmenu $menuGemaModeTitle ]

//
// Time target menu
//
newmenu $menuTimeTargetTitle
menuitemcheckbox (concat (c $colorTimeTargetEnable) "Enable Time Target Mode") [ (isTimeTargetModeActive) ][ setTimeTargetModeActive $arg1 ]
menuitemcheckbox (concat (c 2) "Show countdown") [ (isCountDownModeActive) ][ setCountDownModeActive $arg1 ]
menuitem
menuitemtextinput (concat (c $colorTimeTargetTime) "Minutes") [ (timeTargetGetMinutes) ][ timeTargetSetMinutes $arg1 ]
menuitemtextinput (concat (c $colorTimeTargetTime) "Seconds") [ (timeTargetGetSeconds) ][ timeTargetSetSeconds $arg1 ]
menuitemtextinput (concat (c $colorTimeTargetTime) "Milliseconds  ") [ (timeTargetGetMilliSeconds) ][ timeTargetSetMilliSeconds $arg1 ]
menuitem
menuitem (concat (c $colorClose) "Back") [ showmenu $menuGemaModeTitle ]
